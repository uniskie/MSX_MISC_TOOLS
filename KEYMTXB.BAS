10 SCREEN0:WIDTH80:COLOR15,4,4:DEFINTA-Z
20 '
30 ON STOP GOSUB 300:STOP ON
40 BA=(VDP(3)AND&HF8)*&H40+(VDP(11)AND 3)*&H4000'BLINK TABLE
50 VDP(14)=15 'BLINK OFF
60 FOR I=0 TO 16:A=BA+I*10:FOR J=0 TO 7:VPOKE A+J,255:NEXT:VPOKE A+8,128:VPOKE A+9,0:NEXT:FOR I=10*17 TO 10*24-1:VPOKE BA+I,0:NEXT
70 VDP(14)=16:VDP(13)=&HF0 'BLINK ON
80 '
90 LOCATE 0,0:RESTORE1010
100 FOR I=0 TO 8
110  PRINT"V";
120  FOR J=0 TO 7
130   READ A$:C=LEN(A$):S=(7-C)\2:E=7-C-S
140   PRINTSPACE$(S);A$;SPACE$(E);"V";
150  NEXT
160  PRINT:PRINT
170 NEXT
180 '
190 M0=&HFBE5'KEY MATRIX(NEW)
200 M1=&HFBAD'KEY MATRIX(OLD)
210 DIM MS(7):MS(0)=128:FOR I=1 TO 7:MS(I)=MS(I-1)\2:NEXT
220 FOR I=0 TO 8
230  M=PEEK(M0+I)
240  FOR J=0 TO 7
250   VPOKE BA+I*20+J,255+((M AND MS(J))=0)*127
260  NEXT
270 NEXT
280 GOTO 220
290 '
300 VDP(14)=15 'BLINK OFF
310 END
1000 '
1010 DATA"7","6","5","4","3","2","1","0"
1020 DATA";","[","@","\","^","-","9","8"
1030 DATA"B","A","_","/",".",",","]",":"
1040 DATA"J","I","H","G","F","E","D","C"
1050 DATA"R","Q","P","O","N","M","L","K"
1060 DATA"Z","Y","X","W","V","U","T","S"
1070 DATA"F3","F2","F1","∂≈","CAPS","GRAPH","CTRL","SHIFT"
1080 DATA"RETURN","SELECT","BS","STOP","TAB","ESC","F5","F4"
1090 DATA"–∑ﬁ","º¿","≥¥","À¿ﬁÿ","DEL","INS","HOME","SPACE"
