10 CLEAR:DEFINTA-Z:DIM P(7)
20 COLOR15,0,0:SCREEN1:WIDTH32
30 FORI=0TO255:VPOKE&H1800+I,I:NEXT:LOCATE 0,8,0
40 FOR C=1 TO ASC("_")
50  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
60  ON -(C=10) GOSUB 550'JI(hour)
70  ON -(C=13) GOSUB 510'HYAKU(hundred)
80  ON -(C=29) GOSUB 520'DAI(big)
90  ON -(C=28) GOSUB 520'CROSS
100  ON -((C>16)AND(C<28)) GOTO 130'LINES
110  ON -(C=63) GOSUB 540'?
120  VPOKE AD+0,0:VPOKE AD+1,0
130  VPOKE AD+2,P(0) OR P(1)
140  VPOKE AD+3,P(2)
150  VPOKE AD+4,P(3)
160  VPOKE AD+5,P(4)
170  VPOKE AD+6,P(5) OR P(6)
180  VPOKE AD+7,P(7)
190 NEXT
200 FOR C=ASC("`") TO ASC("~")
210  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
220  ON -(C=105) GOSUB 510'i
230  ON -(C=106) GOSUB 510'j
240  ON -(C=115) GOSUB 530's
250  VPOKE AD+0,0:VPOKE AD+1,0
260  VPOKE AD+2,P(0) OR (P(1)AND127)
270  VPOKE AD+3,P(2) OR (P(1)AND128)
280  VPOKE AD+4,P(3)
290  VPOKE AD+5,P(4)
300  VPOKE AD+6,P(5) OR P(6)
310  VPOKE AD+7,P(7)
320 NEXT
330 FOR C=128 TO 253
340  ON -(C=161) GOTO 470
350  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
360  ON -(C=132) GOSUB 500'CIRCLE
370  ON -(C=133) GOSUB 500'FILLED CIRCLE
380  ON -(C=222) GOSUB 510'DAKUTEN
390  ON -(C=223) GOSUB 510'HANDAKUTEN
400  VPOKE AD+0,0:VPOKE AD+1,0
410  VPOKE AD+2,P(0)
420  VPOKE AD+3,P(1) OR P(2) 
430  VPOKE AD+4,P(3)
440  VPOKE AD+5,P(4) OR P(5)
450  VPOKE AD+6,P(6)
460  VPOKE AD+7,P(7)
470 NEXT
480 BSAVE"FONT8X6.GEN",0,&H7FF,S
490 END
500 P(0)=P(1):P(1)=P(2):RETURN
510 P(3)=P(2):P(2)=P(1):RETURN
520 P(2)=P(2) OR P(1):P(1)=P(0):RETURN
530 P(2)=120:P(4)=112:RETURN
540 P(3)=P(3)OR P(4):P(4)=0:RETURN
550 P(2)=P(2)OR(P(1)AND15):P(1)=(P(1)AND240)OR P(0):P(6)=0:RETURN
