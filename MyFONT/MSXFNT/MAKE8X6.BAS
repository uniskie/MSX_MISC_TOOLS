10 CLEAR:DEFINTA-Z:DIM P(7)
20 COLOR15,0,0:SCREEN1:WIDTH32
30 FORI=0TO255:VPOKE&H1800+I,I:NEXT:LOCATE 0,8,0
40 FOR C=1 TO ASC("_")
50  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
60  ON -(C=13) GOSUB 520'HYAKU(hundred)
61  ON -(C=29) GOSUB 530'DAI(big)
70  ON -(C=28) GOSUB 530'CROSS
80  ON -((C>16)AND(C<28)) GOTO 120'LINES
90  ON -(C=63) GOSUB 550'?
100  VPOKE AD+0,0
110  VPOKE AD+1,0
120  VPOKE AD+2,P(0) OR P(1)
130  VPOKE AD+3,P(2)
140  VPOKE AD+4,P(3)
150  VPOKE AD+5,P(4)
160  VPOKE AD+6,P(5) OR P(6)
170  VPOKE AD+7,P(7)
180 NEXT
190 FOR C=ASC("`") TO ASC("~")
200  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
210  ON -(C=105) GOSUB 520'i
220  ON -(C=106) GOSUB 520'j
230  ON -(C=115) GOSUB 540's
240  VPOKE AD+0,0
250  VPOKE AD+1,0
260  VPOKE AD+2,P(0) OR (P(1)AND127)
270  VPOKE AD+3,P(2) OR (P(1)AND128)
280  VPOKE AD+4,P(3)
290  VPOKE AD+5,P(4)
300  VPOKE AD+6,P(5) OR P(6)
310  VPOKE AD+7,P(7)
320 NEXT
330 FOR C=128 TO 253
340  ON -(C=161) GOTO 480
350  AD=C*8:FOR I=0 TO 7:P(I)=VPEEK(AD+I):NEXT
360  ON -(C=132) GOSUB 510'CIRCLE
370  ON -(C=133) GOSUB 510'FILLED CIRCLE
380  ON -(C=222) GOSUB 520'DAKUTEN
390  ON -(C=223) GOSUB 520'HANDAKUTEN
400  VPOKE AD+0,0
410  VPOKE AD+1,0
420  VPOKE AD+2,P(0)
430  VPOKE AD+3,P(1) OR P(2) 
440  VPOKE AD+4,P(3)
450  VPOKE AD+5,P(4) OR P(5)
460  VPOKE AD+6,P(6)
470  VPOKE AD+7,P(7)
480 NEXT
490 BSAVE"FONT8X6.GEN",0,&H7FF,S
500 END
510 P(0)=P(1):P(1)=P(2):RETURN
520 P(3)=P(2):P(2)=P(1):RETURN
530 P(2)=P(2) OR P(1):P(1)=P(0):RETURN
540 P(2)=120:P(4)=112:RETURN
550 P(3)=P(3)OR P(4):P(4)=0:RETURN

