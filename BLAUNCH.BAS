10 SCREEN0:WIDTH80:KEYOFF:COLOR15,0,0
20 DEFINT A-Z:DIM M$(2),M(17),VX(8),VY(8)
30 X=0:Y=0:Y0=2:YC=23-Y0:YM=0:XC=6:XM=XC-1
40 FOR I=0 TO 8:READ VX(I),VY(I):PRINT".";:NEXT
50 DATA 0,0, 0,-1, 1,-1, 1,0, 1,1, 0,1, -1,1, -1,0, -1,-1
60 ON STOP GOSUB 120:STOP ON:GOTO 150
70 '** PRINT TITLE
80 LOCATE 0,0:PRINT"FILE LOADER for MSX2";
90 IF TR THEN PRINT " [CPU:";M$(CM);"] (CHANGE: Trig-B OR 'M')";
100 PRINT SPC(79-POS(0)):PRINT:RETURN
110 '** STOP TRAP
120 IF TR AND(OM>=0)THEN CM=USR(OM)'restore CPU
130 VDP(14)=0:STOP OFF:END
140 '** CPU CHANGE
150 OM=-1:TR=PEEK(&H180)<>0:IF TR=0 THEN 250 'is exist CHGCPU?
160 AD=&HF975:DEFUSR=AD:RESTORE 180
170 FOR I=0 TO 34:READ A$:POKE AD + I,VAL("&H"+A$):PRINT".";:NEXT
180 DATA FE,02,C0,E5,DD,E1,3A,83,01,B7,28,0F,DD,7E,02,FE
190 DATA 04,30,05,F6,80,CD,80,01,CD,83,01,DD,77,02,DD,36
200 DATA 03,00,C9
210 OM=USR(255):CM=USR(1-(OM=2))'R800(ROM)
220 FOR I=0 TO 2:READ M$(I):PRINT".";:NEXT
230 DATA Z80,R800(ROM),R800(DRAM)
240 '** SET BLINK MODE
250 TA=BASE(0):CA=BASE(1)
260 FOR I=0 TO 239:VPOKE CA+I,0:NEXT
270 VDP(13)=&HF5:VDP(14)=&H21:RESTORE 290
280 FOR I=0 TO XC*3-1:READ A$:M(I)=VAL("&H"+A$):PRINT".";:NEXT
290 DATA FF,F0,00,07,FF,80,3F,FC,00,01,FF,E0,0F,FF,00,7F,F8,00
300 '** START
310 KEY7,CHR$(12)+"run"+CHR$(34)+"autoexec.bas"+CHR$(34)+"chr$(13)
320 DEF FNVA(X,Y)=X*13+(Y+Y0)*80
330 DEF FNV(X,Y,N)=VPEEK(TA+FNVA(X,Y)+N)
340 DEF FNST=STICK(0)OR(STICK(1)*-(STICK(0)=0))
350 DEF FNSA=(S=13)OR STRIG(0)OR STRIG(1)
360 DEF FNSB=(S$="M")OR(S$="m")OR STRIG(2)
370 DEF FNOK=(S$="Y")OR(S$="y")OR(FNST=7)
380 DEF FNNO=(S$="N")OR(S$="n")OR(FNST=3)
390 CLS:IF TR THEN CM=USR(0)
400 GOSUB 80
410 ON ERROR GOTO 720:PRINT:FILES"*":FILES"*.BAS":FILES"*.MUS":ON ERROR GOTO0
420 IF(FNV(0,0,0)=32)THEN BEEP:LOCATE 0,1:PRINT"* File not found *"
430 IF(YM<YC)AND(FNV(0,YM+1,0)<>32)THEN YM=YM+1:GOTO 430
440 GOSUB 830
450 '** LOOP
460 S$=INKEY$:S=ASC(S$+CHR$(0))
470 ST=FNST:IF ST=0 THEN 570 ELSE GOSUB 850
480 '** MOVE CURSOR
490 VX=VX(ST):VY=VY(ST)
500 X=X+VX:Y=Y+VY
510 IF X<0 THEN X=XM:Y=Y-1
520 IF X>XM THEN X=0:Y=Y+1
530 IF Y<0 THEN Y=YM
540 IF Y>YM THEN Y=0
550 IF(FNV(X,Y,0)=32)AND((X>0)OR(Y>0))THEN 500
560 GOSUB 830
570 IF FNSB=0 THEN 610
580  '** CHGCPU
590  CM=1-(OM=2)-CM:CM=USR(CM):GOSUB 80:GOTO 460
600 'DECIDE ?
610 IF FNSA=0 THEN 460' NOT TRIG => LOOP
620 IF(FNV(X,Y,0)=32)THEN BEEP:GOTO 460'BLANK
630 IF(FNV(X,Y,2)=92)THEN BEEP:GOTO 460'\ DRIVE
640 '** DECIDE
650 F$="":FORI=0TO12:F$=F$+CHR$(VPEEK(FNVA(X,Y)+I)):NEXT
660 LOCATE 2,1:PRINT"Run"+CHR$(34)+F$+CHR$(34)+"  Ready?(Y<-/->N)";
670 S$=INKEY$:S=ASC(S$+CHR$(0))
680 IF FNOK THEN PRINT"Y":VDP(14)=0:STOP OFF:RUN F$
690 IF FNNO=0 THEN 670 ELSE PRINT"N":BEEP
700 IF FNNO THEN 700 ELSE LOCATE2,1:PRINTSPC(40):GOTO 460
710 '** ERROR CHECK SUB
720 ER=ERR:A$="":BEEP
730 IF ER=61 THEN A$="Bad File Mode"
740 IF ER=69 THEN A$="Disk I/O Error"
750 IF ER=70 THEN A$="Disk Offline"
760 IF ER=53 THEN RESUME NEXT'File not found -> IGNORE
770 IF A$="" THEN ERROR ER
780 LOCATE 2,1:PRINT SPC(40)
790 LOCATE 2,1:PRINT A$+": (Hit a any key)";
800 IF(FNSA=0)AND(FNSA=0)AND(INKEY$="")THEN 800
810 LOCATE2,1:PRINT SPC(40):RESUME NEXT
820 '** WRITE BLINK
830 A=CA+FNVA(X,Y)\8:C=X*3:FOR I=0 TO 2:B=A+I:VPOKE B,VPEEK(B) XOR M(C+I):NEXT:RETURN
840 '** ERASE BLINK
850 A=CA+FNVA(X,Y)\8:FOR I=0 TO 2:B=A+I:VPOKE B,0:NEXT:RETURN
