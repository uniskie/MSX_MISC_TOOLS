10 SCREEN0,,0:WIDTH80:KEYOFF:COLOR15,0,0
20 DEFINT A-Z:DIM M$(2),M(17),VX(8),VY(8)
30 WT=13:X=0:Y=0:Y0=2:YC=23-Y0:YM=0:XC=6:XM=XC-1
40 FOR I=0 TO 8:READ VX(I),VY(I):PRINT".";:NEXT
50 DATA 0,0, 0,-1, 1,-1, 1,0, 1,1, 0,1, -1,1, -1,0, -1,-1
60 ON STOP GOSUB 120:STOP ON:GOTO 150
70 '** PRINT TITLE
80 LOCATE 0,0,0:PRINT"BASIC×ÝÁ¬° for MSX2";
90 IF TR THEN PRINT " [CPU:";M$(CM);"] (CHANGE:'M'·° or BÎÞÀÝ)";
100 PRINT SPC(79-POS(0)):PRINT:RETURN
110 '** STOP TRAP
120 'IF TR AND(OM>=0)THEN CM=USR(OM)'restore CPU
130 SCREEN,,1:LOCATE,,0:VDP(14)=0:STOP OFF:END
140 '** CPU CHANGE
150 AD=&HF975:OM=-1:TR=PEEK(&H180)<>0:IF TR=0 THEN 270 'is exist CHGCPU?
160 DEFUSR=AD:RESTORE 180
170 READ A$:IF A$<>"*"THEN POKE AD,VAL("&H"+A$):AD=AD+1:PRINT".";:GOTO 170
180 DATA FE,02,C0,E5,DD,E1,3A,83,01,B7,28,0F,DD,7E,02,FE,04,30,05,F6,80,CD,80,01,CD,83,01,DD,77,02,DD,36,03,00,C9,*
190 DEFUSR1=AD:RESTORE 210
200 READ A$:IF A$<>"*"THEN POKE AD,VAL("&H"+A$):AD=AD+1:PRINT".";:GOTO 200
210 DATA FE,02,C0,E5,0E,6F,CD,7D,F3,B7,3E,00,20,01,78,DD,E1,DD,77,02,DD,36,03,00,C9,*
220 DO=USR1(0):IF DO<2 THEN TR=0:OM=0:CM=USR(0):GOTO 270 'DOS1=Z80 only
230 OM=USR(255):CM=USR(1-(OM=2))'R800
240 RESTORE 250:FOR I=0 TO 2:READ M$(I):PRINT".";:NEXT
250 DATA "Z80 ","R800","R800":'Z80,R800 ROM,R800 DRAM
260 '** SET BLINK MODE
270 TA=BASE(0):CA=BASE(1)
280 'FOR I=0 TO 239:VPOKE CA+I,0:NEXT
290 DEFUSR2=AD:RESTORE 310
300 READ A$:IF A$<>"*"THEN POKE AD,VAL("&H"+A$):AD=AD+1:PRINT".";:GOTO 300
310 DATA 2A,B5,F3,01,F0,00,AF,CD,6B,01,C9,*
320 A=USR2(0)'CLEAR
330 VDP(13)=&HF5:VDP(14)=&H21:RESTORE 350
340 FOR I=0 TO XC*3-1:READ A$:M(I)=VAL("&H"+A$):PRINT".";:NEXT
350 DATA FF,F0,00,07,FF,80,3F,FC,00,01,FF,E0,0F,FF,00,7F,F8,00
360 '** START
370 KEY7,CHR$(12)+"run"+CHR$(34)+"autoexec.bas"+CHR$(34)+"chr$(13)
380 DEF FNVA(X,Y)=X*13+(Y+Y0)*80
390 DEF FNV(X,Y,N)=VPEEK(TA+FNVA(X,Y)+N)
400 DEF FNST=STICK(0)OR(STICK(1)*-(STICK(0)=0))
410 DEF FNSA=(S$=CHR$(13))OR STRIG(0)OR STRIG(1)
420 DEF FNSB=(S$="M")OR(S$="m")OR STRIG(3)
430 DEF FNOK=(S$="Y")OR(S$="y")OR FNSA
440 DEF FNNO=(S$="N")OR(S$="n")OR FNSB OR FNST
450 CLS:GOSUB 80:DEF USR9=&H156'KILBUF
460 ON ERROR GOTO 900:PRINT:FILES"*":FILES"*.BAS":FILES"*.MUS":ON ERROR GOTO0
470 IF(YM<YC)AND(FNV(0,YM+1,0)<>32)THEN YM=YM+1:GOTO 470
480 IF(FNV(0,0,0)=32)THEN BEEP:LOCATE 0,1:PRINT"* File not found *"
490 GOSUB 1010:IF TR THEN CM=USR(0):GOSUB 80
500 '** LOOP
510 S$=INKEY$
520 ST=FNST:IF ST=0 THEN 630 ELSE GOSUB 1030:TIME=0
530 '** MOVE CURSOR
540 VX=VX(ST):VY=VY(ST)
550 X=X+VX:Y=Y+VY
560 IF X<0 THEN X=XM:Y=Y-1
570 IF X>XM THEN X=0:Y=Y+1
580 IF Y<0 THEN Y=YM
590 IF Y>YM THEN Y=0
600 IF(FNV(X,Y,0)=32)AND((X>0)OR(Y>0))THEN 550
610 GOSUB 1010
620 S$=INKEY$:IF(TIME<WT)AND FNST THEN 620
630 IF FNSB=0 THEN 680
640  '** CHGCPU
650  IF TR THEN CM=1-(OM=2)-CM:CM=USR(CM):GOSUB 80
660  S$=INKEY$:IF FNSB THEN 660 ELSE 510
670 'DECIDE ?
680 IF FNSA=0 THEN 510' NOT TRIG => LOOP
690 IF(FNV(X,Y,0)=32)THEN GOTO 510'BLANK
700 IF(FNV(X,Y,2)=92)THEN GOTO 510'\ DRIVE
710 '** DECIDE
720 VDP(14)=&H10
730 'F$="":FORI=0TO11:F$=F$+CHR$(VPEEK(FNVA(X,Y)+I)):NEXT:GOSUB 1000
740 IF TR THEN A=USR(1-(OM=2))
750 F$="":SOUND8,0:SOUND7,190:TIME=0:FORI=0TO11
760  IF TIME<(I*2) THEN 760
770  SOUND1,1-(I AND 1):SOUND0,120-I*4:SOUND8,12-I
780  F$=F$+CHR$(FNV(X,Y,I))
790  IF I>4 THEN VPOKE CA+5+I,255
800 NEXT:IF TR THEN A=USR(CM)
810 LOCATE 1,1:PRINT"RUN"+CHR$(34)+F$+CHR$(34)+":Ready? (Y=AÎÞÀÝ=OK:N=BÎÞÀÝ=CANCEL) ";
820 S$=INKEY$:IF FNSA THEN 820 ELSE LOCATE,,1 'µ¼¯ÊßÅ¼¶²Ë
830 S$=INKEY$
840 IF FNOK THEN LOCATE,,0:PRINT"Y":VDP(14)=0:STOP OFF:CLS:PRINT F$:RUN F$
850 IF FNNO=0 THEN 830 ELSE LOCATE,,0:PRINT"N":GOSUB 1060
860 S$=INKEY$
870 IF FNNO THEN 860 ELSE LOCATE1,1:PRINTSPC(60)
880 VDP(14)=&H21:FOR I=10 TO 19:VPOKE CA+I,0:NEXT:GOTO 510
890 '** ERROR CHECK SUB
900 ER=ERR:A$=""
910 IF ER=61 THEN A$="Bad File Mode"
920 IF ER=69 THEN A$="Disk I/O Error"
930 IF ER=70 THEN A$="Disk Offline"
940 IF ER=53 THEN RESUME NEXT'File not found -> IGNORE
950 IF A$="" THEN ERROR ER
960 LOCATE 2,1:PRINT SPC(40):BEEP
970 LOCATE 2,1:PRINT A$+": (Hit a any key)";:A=USR9(0)
980 IF(FNSA=0)AND(FNSA=0)AND(INKEY$="")THEN 980
990 LOCATE2,1:PRINT SPC(40):RESUME NEXT
1000 '** WRITE BLINK
1010 A=CA+FNVA(X,Y)\8:C=X*3:FOR I=0 TO 2:B=A+I:VPOKE B,VPEEK(B) XOR M(C+I):NEXT:RETURN
1020 '** ERASE BLINK
1030 A=CA+FNVA(X,Y)\8:FOR I=0 TO 2:B=A+I:VPOKE B,0:NEXT:RETURN
1040 '** SE
1050 SOUND8,0:SOUND7,190:TIME=0:FORI=0TO20:I=TIME:SOUND1,1-(I AND 1):SOUND0,120-I*4:SOUND8,10-I\2:NEXT:RETURN
1060 SOUND8,0:SOUND7,190:TIME=0:FORI=0TO20:I=TIME:SOUND1,2-(I AND 1):SOUND0,20+I*4:SOUND8,10-I\2:NEXT:RETURN

