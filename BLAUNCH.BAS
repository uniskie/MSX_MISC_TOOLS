10 SCREEN0:WIDTH80:KEYOFF:COLOR15,0,0
20 DEFINT A-Z:DIM M$(2),M(17),VX(3),VY(3)
30 X=0:Y=0:Y0=2:YC=23-Y0:YM=0:XC=6:XM=XC-1
40 FOR I=0 TO 3:READ VX(I),VY(I):NEXT
50 DATA 1,0, -1,0, 0,-1, 0,1
60 GOTO 130
70 '** PRINT TITLE
80 LOCATE 0,0:PRINT"FILE LOADER for MSX2";
90 PRINT " [CPU:";M$(CM);"] (0=Z80/1=R800ROM/2=R800DRAM)":RETURN
100 '** STOP BLINK
110 VDP(14)=0:STOP OFF:END
120 '** CPU CHANGE
130 AD=&HF975:DEFUSR=AD
140 FOR I=0 TO 34:READ A$:POKE AD + I,VAL("&H"+A$):PRINT".";:NEXT
150 DATA FE,02,C0,E5,DD,E1,3A,83,01,B7,28,0F,DD,7E,02,FE
160 DATA 04,30,05,F6,80,CD,80,01,CD,83,01,DD,77,02,DD,36
170 DATA 03,00,C9
180 CM=USR(255):CM=USR(1-(CM=2))'R800(ROM)
190 FOR I=0 TO 2:READ M$(I):PRINT".";:NEXT
200 DATA Z80,R800(ROM),R800(DRAM)
210 '** SET BLINK MODE
220 TA=BASE(0):CA=BASE(1)
230 FOR I=0 TO 239:VPOKE CA+I,0:NEXT
240 VDP(13)=&HF5:VDP(14)=&H21
250 FOR I=0 TO XC*3-1:READ A$:M(I)=VAL("&H"+A$):PRINT".";:NEXT
260 DATA FF,F0,00,07,FF,80,3F,FC,00,01,FF,E0,0F,FF,00,7F,F8,00
270 ON STOP GOSUB 110:STOP ON
280 '** START
290 KEY7,CHR$(12)+"run"+CHR$(34)+"autoexec.bas"+CHR$(34)+"chr$(13)
300 DEF FNA(X,Y)=X*13+(Y+Y0)*80
310 DEF FNV(X,Y)=VPEEK(TA+FNA(X,Y))
320 CLS:CM=USR(0):GOSUB 80
330 ONERRORGOTO600:PRINT:FILES"*":FILES"*.BAS":FILES"*.MUS":ONERRORGOTO0
340 IF(FNV(0,0)=32)THEN BEEP:LOCATE 0,1:PRINT"* File not found *"
350 IF(YM<YC)AND(FNV(0,YM+1)<>32)THEN YM=YM+1:GOTO 350
360 GOSUB 710
370 A$=INKEY$:IF A$="" THEN S=-1 ELSE S=ASC(A$)-28
380 IF(S<0)OR(S>3)THEN 490
390 GOSUB 730
400 '** LOOP
410 VX=VX(S):VY=VY(S)
420 X=X+VX:Y=Y+VY
430 IF X<0 THEN X=XM:Y=Y-1
440 IF X>XM THEN X=0:Y=Y+1
450 IF Y<0 THEN Y=YM
460 IF Y>YM THEN Y=0
470 IF(FNV(X,Y)=32)AND(X>0)AND(Y>0) THEN 420
480 GOSUB 710
490 IF(S<20)OR(S>22)THEN 510
500 CM=USR(S-20):GOSUB 80:GOTO 370
510 IF STRIG(0)=0 THEN370
520 '** DECIDE
530 IF(FNV(X,Y)=32)THEN BEEP:GOTO 370
540 F$="":FORI=0TO12:F$=F$+CHR$(VPEEK(FNA(X,Y)+I)):NEXT
550 LOCATE 2,1:PRINT"Run"+CHR$(34)+F$+CHR$(34)+"  Ready?(Y/N)";
560 A$=INPUT$(1)
570 IF A$="N" OR A$="n" THEN PRINT A$:BEEP:LOCATE2,1:PRINTSPC(40):GOTO 370
580 IF A$="Y" OR A$="y" THEN PRINT A$:RUN F$
590 GOTO 560
600 '** ERROR CHECK SUB
610 ER=ERR:A$="":BEEP
620 IF ER=61 THEN A$="Bad File Mode (basic file ¼Þ¬ Å²)"
630 IF ER=69 THEN A$="Disk I/O Error (Disk ¶Þ µ¶¼²)"
640 IF ER=70 THEN A$="Disk Offline (Disk É ¼Þ­ÝËÞ ¶Þ ÃÞ·Ã²Å²)"
650 IF ER=53 THEN RESUME NEXT ELSE ERROR ER
660 LOCATE 2,1:PRINT SPC(40)
670 LOCATE 2,1:PRINT A$+" -Hit a any key-";
680 A$=INPUT$(1):LOCATE2,1:PRINT SPC(40)
690 GOTO 370
700 '** WRITE BLINK
710 A=CA+FNA(X,Y)\8:C=X*3:FOR I=0 TO 2:B=A+I:VPOKE B,VPEEK(B) XOR M(C+I):NEXT:RETURN
720 '** ERASE BLINK
730 A=CA+FNA(X,Y)\8:FOR I=0 TO 2:B=A+I:VPOKE B,0:NEXT:RETURN
