10 SCREEN0,,0:WIDTH80:KEYOFF:COLOR15,0,0
20 DEFINT A-Z:DIM M$(2),M(17),VX(8),VY(8)
30 WT=13:X=0:Y=0:Y0=2:YC=23-Y0:YM=0:XC=6:XM=XC-1
40 FOR I=0 TO 8:READ VX(I),VY(I):PRINT".";:NEXT
50 DATA 0,0, 0,-1, 1,-1, 1,0, 1,1, 0,1, -1,1, -1,0, -1,-1
60 ON STOP GOSUB 120:STOP ON:GOTO 150
70 '** PRINT TITLE
80 LOCATE 0,0,0:PRINT"BASIC×ÝÁ¬° for MSX2";
90 IF TR THEN PRINT " [CPU:";M$(CM);"] (CHANGE:'M'·° or BÎÞÀÝ)";
100 PRINT SPC(79-POS(0)):PRINT:RETURN
110 '** STOP TRAP
120 IF TR AND(OM>=0)THEN CM=USR(OM)'restore CPU
130 SCREEN,,1:LOCATE,,0:VDP(14)=0:STOP OFF:END
140 '** CPU CHANGE
150 OM=-1:TR=PEEK(&H180)<>0:IF TR=0 THEN 250 'is exist CHGCPU?
160 AD=&HF975:DEFUSR=AD:RESTORE 180
170 FOR I=0 TO 34:READ A$:POKE AD + I,VAL("&H"+A$):PRINT".";:NEXT
180 DATA FE,02,C0,E5,DD,E1,3A,83,01,B7,28,0F,DD,7E,02,FE
190 DATA 04,30,05,F6,80,CD,80,01,CD,83,01,DD,77,02,DD,36
200 DATA 03,00,C9
210 OM=USR(255):CM=USR(1-(OM=2))'R800(ROM)
220 FOR I=0 TO 2:READ M$(I):PRINT".";:NEXT
230 DATA "Z80 ","R800","R800":'Z80,R800 ROM,R800 DRAM
240 '** SET BLINK MODE
250 TA=BASE(0):CA=BASE(1)
260 FOR I=0 TO 239:VPOKE CA+I,0:NEXT
270 VDP(13)=&HF5:VDP(14)=&H21:RESTORE 290
280 FOR I=0 TO XC*3-1:READ A$:M(I)=VAL("&H"+A$):PRINT".";:NEXT
290 DATA FF,F0,00,07,FF,80,3F,FC,00,01,FF,E0,0F,FF,00,7F,F8,00
300 '** START
310 KEY7,CHR$(12)+"run"+CHR$(34)+"autoexec.bas"+CHR$(34)+"chr$(13)
320 DEF FNVA(X,Y)=X*13+(Y+Y0)*80
330 DEF FNV(X,Y,N)=VPEEK(TA+FNVA(X,Y)+N)
340 DEF FNST=STICK(0)OR(STICK(1)*-(STICK(0)=0))
350 DEF FNSA=(S$=CHR$(13))OR STRIG(0)OR STRIG(1)
360 DEF FNSB=(S$="M")OR(S$="m")OR STRIG(3)
370 DEF FNOK=(S$="Y")OR(S$="y")OR FNSA
380 DEF FNNO=(S$="N")OR(S$="n")OR FNSB OR FNST
390 CLS:GOSUB 80:DEF USR9=&H156'KILBUF
400 ON ERROR GOTO 830:PRINT:FILES"*":FILES"*.BAS":FILES"*.MUS":ON ERROR GOTO0
410 IF(YM<YC)AND(FNV(0,YM+1,0)<>32)THEN YM=YM+1:GOTO 410
420 IF(FNV(0,0,0)=32)THEN BEEP:LOCATE 0,1:PRINT"* File not found *"
430 GOSUB 940:IF TR THEN CM=USR(0):GOSUB 80
440 '** LOOP
450 S$=INKEY$
460 ST=FNST:IF ST=0 THEN 570 ELSE GOSUB 960:TIME=0
470 '** MOVE CURSOR
480 VX=VX(ST):VY=VY(ST)
490 X=X+VX:Y=Y+VY
500 IF X<0 THEN X=XM:Y=Y-1
510 IF X>XM THEN X=0:Y=Y+1
520 IF Y<0 THEN Y=YM
530 IF Y>YM THEN Y=0
540 IF(FNV(X,Y,0)=32)AND((X>0)OR(Y>0))THEN 490
550 GOSUB 940
560 S$=INKEY$:IF(TIME<WT)AND FNST THEN 560
570 IF FNSB=0 THEN 620
580  '** CHGCPU
590  CM=1-(OM=2)-CM:CM=USR(CM):GOSUB 80
600  S$=INKEY$:IF FNSB THEN 600 ELSE 450
610 'DECIDE ?
620 IF FNSA=0 THEN 450' NOT TRIG => LOOP
630 IF(FNV(X,Y,0)=32)THEN GOTO 450'BLANK
640 IF(FNV(X,Y,2)=92)THEN GOTO 450'\ DRIVE
650 '** DECIDE
660 VDP(14)=&H10
670 'F$="":FORI=0TO11:F$=F$+CHR$(VPEEK(FNVA(X,Y)+I)):NEXT:GOSUB 1000
680 A=USR(1-(OM=2)):F$="":SOUND8,0:SOUND7,190:TIME=0:FORI=0TO11
690  IF TIME<(I*2) THEN 690
700  SOUND1,1-(I AND 1):SOUND0,120-I*4:SOUND8,12-I
710  F$=F$+CHR$(FNV(X,Y,I))
720  IF I>4 THEN VPOKE CA+5+I,255
730 NEXT:A=USR(CM)
740 LOCATE 1,1:PRINT"RUN"+CHR$(34)+F$+CHR$(34)+":Ready? (Y=AÎÞÀÝ=OK:N=BÎÞÀÝ=CANCEL) ";
750 S$=INKEY$:IF FNSA THEN 750 ELSE LOCATE,,1 'µ¼¯ÊßÅ¼¶²Ë
760 S$=INKEY$
770 IF FNOK THEN LOCATE,,0:PRINT"Y":VDP(14)=0:STOP OFF:CLS:PRINT F$:RUN F$
780 IF FNNO=0 THEN 760 ELSE LOCATE,,0:PRINT"N":GOSUB 990
790 S$=INKEY$
800 IF FNNO THEN 790 ELSE LOCATE1,1:PRINTSPC(60)
810 VDP(14)=&H21:FOR I=10 TO 19:VPOKE CA+I,0:NEXT:GOTO 450
820 '** ERROR CHECK SUB
830 ER=ERR:A$=""
840 IF ER=61 THEN A$="Bad File Mode"
850 IF ER=69 THEN A$="Disk I/O Error"
860 IF ER=70 THEN A$="Disk Offline"
870 IF ER=53 THEN RESUME NEXT'File not found -> IGNORE
880 IF A$="" THEN ERROR ER
890 LOCATE 2,1:PRINT SPC(40):BEEP
900 LOCATE 2,1:PRINT A$+": (Hit a any key)";:A=USR9(0)
910 IF(FNSA=0)AND(FNSA=0)AND(INKEY$="")THEN 910
920 LOCATE2,1:PRINT SPC(40):RESUME NEXT
930 '** WRITE BLINK
940 A=CA+FNVA(X,Y)\8:C=X*3:FOR I=0 TO 2:B=A+I:VPOKE B,VPEEK(B) XOR M(C+I):NEXT:RETURN
950 '** ERASE BLINK
960 A=CA+FNVA(X,Y)\8:FOR I=0 TO 2:B=A+I:VPOKE B,0:NEXT:RETURN
970 '** SE
980 SOUND8,0:SOUND7,190:TIME=0:FORI=0TO20:I=TIME:SOUND1,1-(I AND 1):SOUND0,120-I*4:SOUND8,10-I\2:NEXT:RETURN
990 SOUND8,0:SOUND7,190:TIME=0:FORI=0TO20:I=TIME:SOUND1,2-(I AND 1):SOUND0,20+I*4:SOUND8,10-I\2:NEXT:RETURN
