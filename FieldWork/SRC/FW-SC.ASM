;;-- KANJI EDITOR "FIELD WORK" --

;  SCREEN FUNCTIONS

;  _SAVE("FW-SC

; VDP CONSTANTS
RYSCR:	EQU		23+80H
R_PAT:	EQU		4 +80H
R_COL:	EQU		10+80H
R_NAM:	EQU		2 +80H
R_SPT:	EQU		6 +80H
R_SNM:	EQU		5 +80H

;--- BIOS WORK AREA ---
H_KEYI:	EQU		0FD9AH

RG8SAV: EQU		0FFE7H	; VDP#8

;--- CONSTANTS ---
SCWIDTH:	EQU		32		; SCREEN WIDTH 32
SCR_TOP:	EQU		0		; SCROLL DEFAULT
UD_SCR:		EQU		22		; SCROLL MAX

;--- WORK AREA ---
SPCFLG:	DB		0	;·ºÞ³ DISPLAY OFF
LFTCNT:	DB		0	;lf

;  (LFCHCK) GROBAL RUTINE
;     from KANJI_PC ("KJ-F.ASF")
;== KANJI 16*16 for SCREEN2 ==

LFCHCK:	LD		(PR_AXS+2),A
LFCHCK_:
		LD		BC,(PR_AXS)
		ADD		A,C
		CP		32+1
		RET		C
		CALL	CD_RET_
		JP		CD_LF

INCAXS:	LD		HL,(PR_AXS)
		LD		A,(PR_AXS+2)
		ADD		A,L
		LD		(PR_AXS),A
		RET

PRINT:	LD		A,(DE)
		CP		TAB
		JP		Z,PRTTAB	;PRINT_
		CP		' '
		JR		C,CONTRL
PRINT_:	CALL	KANJI_S
		CALL	INCAXS
		JP		PRINT

;<---
PRTTAB:	LD		A,C_BKOF
		CALL	CSETTMP
		CALL	KANJI_S
		CALL	CRESTMP
		CALL	INCAXS
		JP		PRINT
;--->

CONTRL:	XOR		A
		LD		(PR_AXS+2),A
		LD		A,(DE)
		INC		DE
		OR		A
		RET		Z
		CP		EOF
		JP		Z,CT_EOF
		LD		HL,PRINT
		PUSH	HL
		CP		10
		JP		Z,CD_LF
		CP		13
		RET		NZ
		LD		HL,CD_RETC
		PUSH	HL
LF_CCK:
		LD		A,1
		LD		(PR_AXS+2),A
		PUSH	DE
		CALL	LFCHCK
		POP		DE
		RET

CT_EOF:	LD		A,(SPCFLG)
		OR		A
		RET		NZ
		LD		A,1
		CALL	LFCHCK_
		CALL	SPCLIN
		LD		HL,3A5H+24
		LD		A,HANKA_X
		LD		(KJCL_F),A
		CALL	KANJI_PC
		XOR		A
		LD		(KJCL_F),A
CTEOF_:	XOR		A
		LD		(PR_AXS),A
		LD		A,(PR_AXS+1)
		CP		UD_SCR
		RET		NC
		ADD		A,2
		LD		(PR_AXS+1),A
		CALL	SPCLIN
		JP		CTEOF_

; - TEMPORARY COLOR 
CSETTMP:EX		AF,AF'
		LD		A,(FONTCOL)
		LD 		(CNRET_+1),A
		EX		AF,AF'
		LD		(FONTCOL),A
		RET
CRESTMP:
		LD 		A,(CNRET_+1)
		LD		(FONTCOL),A
		RET

; - LF FORCE INTERRUPTION
; Stop displaying when
; specified number of LF appear
LFSET:	LD		(LFTCNT),A
		LD		(CONRET+1),SP
		LD		A,(FONTCOL)
		LD		(CNRET_+1),A
		; SP Ê PRINT ÃÞ Î¿ÞÝ ¼À Î³¶Þ ±Ý¾ÞÝ
		RET
LFRSET:	XOR		A
		LD		(LFTCNT),A
		RET
CONRET:	LD		SP,0000		;dummy
CNRET_:	LD		A,0			;dummy
		LD		(FONTCOL),A
		EXX
		LD		A,(PR_AXS+2)
		SUB		1
		RET		C
		DEC		DE
		RET		Z
		DEC		DE
		RET

CD_RETC:
		LD		A,(SPCFLG)
		OR		A
		JR		NZ,CD_RET
		CALL	SPCLIN
		LD		HL,3A2H
		LD		A,HANKA_X
		LD		(KJCL_F),A
		CALL	KANJI_PC
		XOR		A
		LD		(KJCL_F),A
		JP		CD_RET
CD_RET_:
		LD		A,(SPCFLG)
		OR		A
		JR		NZ,CD_RET
		CALL	SPCLIN
CD_RET:	XOR		A
		LD		(PR_AXS),A
		RET

CD_LF:	EXX
		LD		A,(LFTCNT)
		SUB		1
		JR		C,_CD_LF
		LD		(LFTCNT),A
		JR		Z,CONRET
_CD_LF:	LD		A,(PR_AXS+1)
		INC		A
		INC		A
		CALL	SCRL_LF
		LD		(PR_AXS+1),A
		EXX
		RET

CD_CLS:	LD		HL,0
		LD		(PR_AXS),HL
		LD		A,SCR_TOP
		LD		(SCRL_L),A
		XOR		A
		DI
		OUT		(99H),A
		LD		A,40H
		OUT		(99H),A
		EI
		LD		BC,98H
		XOR		A
		LD		L,4
		CALL	L_CLS
		INC		A
		LD		L,4
L_CLS:	OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		OUT		(C),A
		DJNZ	L_CLS
		DEC		L
		JP		NZ,L_CLS
		RET

;--- FILL NO-PRINTING AREA ---
SPCLIN:	LD		A,(PR_AXS)
		CP		32
		RET		NC
		EXX
		LD		HL,(PR_AXS)
		LD		A,(SCRL_L)
		ADD		A,H
		AND		31
		LD		H,A

		LD		A,L
		AND		31
		RLCA
		RLCA
		RLCA
		LD		L,A
		NEG
		CP		1
		RRA
		LD		E,A
		LD		C,98H

		CALL	VRAM1
		LD		B,E
L_SPCL0:
		LD		A,0AAH
		OUT		(C),A
		LD		A,55H
		OUT		(C),A
		DJNZ	L_SPCL0
		CALL	VRAM2
		LD		B,E
L_SPCL1:
		LD		A,0AAH
		OUT		(C),A
		LD		A,55H
		OUT		(C),A
		DJNZ	L_SPCL1
		LD		D,C_BKOF
		SLA		E
		JP		FONT_C

;--- INSERT DOWNSIDE LINE
SCRL_LF:
		CP		UD_SCR+2
		RET		C
SCRL_UP:
		LD		A,(SCRL_L)
		ADD		A,2
		AND		31
		LD		(SCRL_L),A
;		ADD		A,UD_SCR
;		AND		31
;		CALL	LINECLR
		LD		A,UD_SCR
		RET

;--- INSERT UPSIDE LINE
SCRL_DW:
		LD		A,(SCRL_L)
		SUB		2
		AND		31
		LD		(SCRL_L),A
;		SUB		2
;		AND		31
;		CALL	LINECLR
		RET

;LINECLR:
;		LD		BC,99H
;		DI
;		OUT		(C),B
;		OR		40H
;		OUT		(C),A
;		EI
;		XOR		A
;		DEC		C
;		LD		B,64
;_LINCR:
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		NOP
;		OUT		(C),A
;		DJNZ	L_LINCR
;		RET

SC2INI:
		LD		BC,0899H
		LD		HL,D_SC2I
		DI
		OTIR
		EI

;		COLOR 0 PALETTE
		LD		C,9AH
		OUTI
		OUTI

		; COLOR TABLE 2000H
		DI
		XOR		A
		OUT		(C),A
		LD		A,20H+40H
		OUT		(C),A
		EI
		
		LD		DE,2000H
		LD		BC,1198H
SC2INI1:
		OUT		(C),B
		DEC		E
		JR		NZ,SC2INI1
		DEC		D
		JR		NZ,SC2INI1

		RET

D_SC2I:	; 98H
		DB		8+2,8+80H		;SPRITE OFF
		DB		29H,8+80H		;COLOR0:PLT
		DB		1,7+80H			;BACK COLOR
		DB		0,16+80H		;PALETTE No.0
		; 9AH
		DB		02H,02H			;COLOR0 PALETTE

SC2SET:	LD		BC,0A99H
		LD		HL,D_SC2K
		DI
		OTIR
		EI

		CALL	PAGE1
		LD		DE,1800H
		CALL	VSETW
		DEC		C
		XOR		A
		LD		B,4
L_SC2ST:
		OUT		(C),A
		INC		A
		JP		NZ,L_SC2ST
		DJNZ	L_SC2ST
		LD		DE,800H		;SPRITE PAT&NAME
		CALL	VSETW
		LD		BC,98H
		LD		DE,880H
L_SC2S:	OUT		(C),B
		DEC		DE
		LD		A,D
		OR		E
		JP		NZ,L_SC2S
		CALL	SC2SET2
		CALL	PAGE0
		CALL	CD_CLS
		CALL	SETWIN
		RET

D_SC2K:	DB		16H,R_NAM		;PAT_NAME:5800H
		DB		09H,R_SPT		;SPR_PAT.:4800H
		DB		0A0H,R_SNM		;SPR_NAME:5000H
		DB		128,9+80H		;Y:212
		DB		BACKGC,7+80H	;BACK COLOR

;=== WINDOW ===
SETWIN:	LD		A,(0F3DFH)
		LD		(HSYNCOF),A
		OR		10H
		LD		(HSYNCON),A
		LD		HL,H_KEYI
		LD		DE,O_KEYI
		LD		BC,5
		LDIR
		LD		A,0C3H
		LD		HL,WINDOW
		DI
		LD		(H_KEYI),A
		LD		(H_KEYI+1),HL
		EI
		RET

;== INTERRUPT RESET ==
RESWIN:	DI
		LD		HL,O_KEYI
		LD		DE,H_KEYI
		LD		BC,5
		LDIR
		LD		A,(HSYNCOF)
		OUT		(99H),A
		LD		A,0+80H
		OUT		(99H),A
		LD		A,1
		OUT		(99H),A
		LD		A,15+80H
		OUT		(99H),A
		NOP
		IN		A,(99H)
		XOR		A
		OUT		(99H),A
		LD		A,15+80H
		OUT		(99H),A
		EI
		RET

;== INTERRUPT RUTINE ==
WINDOW:	LD		C,99H
		LD		A,1
		OUT		(C),A
		LD		A,15+80H
		OUT		(C),A
		IN		A,(C)
		RRA
		LD		A,0
		OUT		(C),A
		LD		A,15+80H
		OUT		(C),A
		JR		NC,WIN_OFF

WIN_ON:	LD		HL,WINDON
		EX		(SP),HL
		EX		(SP),HL
		EX		(SP),HL
		EX		(SP),HL
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		LD		A,(TIMERC)
		INC		A
		LD		(TIMERC),A
		RET

WIN_OFF:
		LD		A,(SCRL_L)
		LD		HL,(SCRL_OL)
		LD		(SCRL_OL),A
		RLCA
		RLCA
		RLCA
		RLC		L
		RLC		L
		RLC		L
		CP		L
		JR		Z,WINOF_
		SUB		A,L
		SRA		A
		ADD		A,L
WINOF_:	LD		L,A
		INC		A
		OUT		(C),A
		LD		A,RYSCR
		OUT		(C),A
		LD		A,L
		ADD		A,192-1-3
		OUT		(C),A
		LD		A,19+80H
		OUT		(C),A
		LD		HL,WINDOF
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
		OUTI
SYNCDW:	LD		HL,HSYNCON
		OUTI
		OUTI
		LD		A,(KEYDAT+6)
		AND		16+7	;[KANA]+[GRAPH]+[CTRL]+[SHIFT]
		JP		Z,RETURN

O_KEYI:	DB		0C9H,0,0,0,0

HS_STOP:
		LD		HL,HSYNCOF
		JP		HS_
HS_CONT:
		LD		HL,HSYNCON
HS_:	LD		(SYNCDW+1),HL
		PUSH	BC
		PUSH	AF
		DI
		LD		C,99H
		CALL	WIN_OFF
		EI
		POP		AF
		POP		BC
		RET

SCRL_OL:
		DB		0
TIMERC:	DB		00
WINFLG:	DB		00
WINDOF:	DB		3,R_PAT,0,R_COL,16H,R_NAM
HSYNCON:
		DB		12H,0+80H		;HSYNC ON
; PAT:0000H COL:2000H NAM:5800H
WINDON:	DB		20H,1+80H		;SCREEN OFF
WINSCR:	DB		64+12,RYSCR
		DB		8+3,R_PAT,1,R_COL,17H,R_NAM
		DB		60H,1+80H		;SCREEN ON
HSYNCOF:
		DB		02H,0+80H		;HSYNC OFF
; PAT:4000H COL:6000H NAM:5C00H

PAGE0:	XOR		A
		DI
		OUT		(99H),A
		LD		A,14+80H
		OUT		(99H),A
		EI
		RET
PAGE1:	LD		A,1
		DI
		OUT		(99H),A
		LD		A,14+80H
		OUT		(99H),A
		EI
		RET

;--- WINDOW GRAPHIC SET ---

;-- PATTERN --
SC2SET2:
		LD		DE,0
		CALL	VSETW
		LD		HL,TA_HEAD
		LD		A,7FH
LSC2S21:
		LD		B,8
		OTIR
		DEC		A
		JP		NZ,LSC2S21
		LD		BC,098H
LSC2S2F:
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		DJNZ	LSC2S2F
;-- COLOR --
		LD		DE,2000H
		CALL	VSETW
		LD		BC,7F98H
LSC2S22:
		LD		A,C_INF1
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		NOP
		OUT		(C),A
		LD		A,C_INF2
		OUT		(C),A
		NOP
		OUT		(C),A
		DJNZ	LSC2S22
		LD		BC,C_INF3*256+98H
		LD		DE,208H
LSC2S23:
		OUT		(C),B
		DEC		DE
		LD		A,E
		OR		D
		JP		NZ,LSC2S23
;-- NAME --
		LD		DE,1C00H
		CALL	VSETW
		LD		A,7FH
		LD		BC,0098H
LSC2S24:
		OUT		(C),A
		DJNZ	LSC2S24
		LD		DE,1C40H
		CALL	VSETW
		LD		A,80H
LSC2S25:
		OUT		(C),A
		INC		A
		CP		0C0H
		JP		NZ,LSC2S25
		RET

VSETW:	LD		A,E
		DI
		OUT		(99H),A
		LD		A,D
		AND		3FH
		OR		40H
		OUT		(99H),A
		EI
		RET


;--- STB WORK AREA ---
; what's STB ? ->  see STB.TXT

CL_BACK:	DW		0	; COLOR BACKUP
WINY:		EQU		4
WINX:		EQU		2
CR_WIN:		DB		WINX,WINY	; STATUS BAR DRAW AXIS

;--- PRINT STB ---
; (Screen image Text Block) from MSX-JE
; IN  | HL:STB-DATA ADDRESS
;

STBSET:	EX		DE,HL
		CALL	PAGE1
		LD		A,1
		LD		(SCRL_F),A
		LD		A,(FONTCOL)
		LD		(CL_BACK),A
		LD		A,(CL_BACK+1)
		CALL	KANJI_C
		LD		HL,(CR_WIN)
		LD		(PR_AXS),HL
		LD		B,1
		CALL	REVSTB
		CALL	PUTSTB
STBRES:	XOR		A
		LD		(SCRL_F),A
		LD		A,(FONTCOL)
		LD		(CL_BACK+1),A
		LD		A,(CL_BACK)
		CALL	KANJI_C
		LD		HL,(PR_AXS)
		LD		(CR_WIN),HL
		LD		A,L
		CP		1
		JR		Z,STBED
		CP		30
		JR		Z,STBED
		LD		B,1
		CALL	REVSTB
STBED:	CALL	PAGE0
		RET

;--- PRINT STB SUB ---
; (Screen image Text Block) from MSX-JE
; IN  | DE:STB DATA ADDRESS
;

PUTSTB:	LD		A,(DE)
STB_FLG:
		CP		0
		RET		Z
		CP		32
		JP		C,CTRSTB
		CP		127
		INC		DE
		JP		Z,PUTSTB
		DEC		DE
		CALL	KANJI_S
		CALL	INCAXS
		JP		PUTSTB

CTRSTB:	LD		HL,PUTSTB
		PUSH	HL
		INC		DE

		CP		CLS
		JP		Z,CLRSTB
		CP		5
		JP		Z,LIDSTB
		CP		BS
		JP		Z,BKSSTB
		CP		16
		RET		C
		EX		DE,HL
		LD		B,(HL)	;NEED +1byte
		EX		DE,HL
		INC		DE
		CP		INS
		JP		Z,REVSTB
		CP		EOF
		JP		Z,COLSTB
		CP		SELECT
		JP		Z,AXXSTB
;		CP		25
;		JP		Z,AXYSTB
		RET

CLRSTB:	LD		A,WINX
		LD		(PR_AXS),A

LIDSTB:	EXX
		LD		H,WINY
		LD		A,(PR_AXS)
		RLCA
		RLCA
		RLCA
		LD		L,A
		NEG
		SUB		16
		LD		E,A
		JP		STBFIL

BKSSTB:	LD		A,(PR_AXS)
		DEC		A
		LD		(PR_AXS),A
		EXX
		LD		H,WINY
		RLCA
		RLCA
		RLCA
		LD		L,A
		LD		E,8
		JP		STBFIL

REVSTB:	LD		A,(PR_AXS)
		CP		WINX
		RET		C
		CP		32-WINX
		RET		NC
		AND		31
		RLCA
		RLCA
		RLCA
LREVSTB:
		PUSH	BC
		DI
		OUT		(99H),A
		EX		AF,AF'
		LD		A,WINY
		OUT		(99H),A
		EI
		LD		BC,898H
		LD		HL,BUF
		INIR
		EX		AF,AF'
		DI
		OUT		(99H),A
		EX		AF,AF'
		LD		A,WINY+40H
		OUT		(99H),A
		EI
		LD		B,8
		LD		HL,BUF
LREV0:	LD		A,(HL)
		INC		HL
		CPL
		OUT		(C),A
		DJNZ	LREV0
		EX		AF,AF'
		DI
		OUT		(99H),A
		EX		AF,AF'
		LD		A,WINY+1
		OUT		(99H),A
		EI
		LD		B,8
		LD		HL,BUF
		INIR
		EX		AF,AF'
		DI
		OUT		(99H),A
		EX		AF,AF'
		LD		A,WINY+41H
		OUT		(99H),A
		EI
		LD		B,8
		LD		HL,BUF
LREV1:	LD		A,(HL)
		CPL
		OUT		(C),A
		INC		HL
		DJNZ	LREV1
		EX		AF,AF'
		ADD		A,8
		POP		BC
		DJNZ	LREVSTB
		RET

COLSTB:	LD		A,B
		CP		1FH
		LD		B,C_KJOF
		JP		Z,_COLSTB
		CP		0F5H
		LD		B,C_KJON
		JP		NZ,KANJI_C
_COLSTB:
		LD		A,B
		JP		KANJI_C

AXXSTB:	LD		A,WINX-1
		ADD		A,B
		LD		(PR_AXS),A
		RET

STBFIL:	CALL	VRAM1
		LD		B,E
		XOR		A
STBFIL0:
		OUT		(98H),A
		DJNZ	STBFIL0
		CALL	VRAM2
		LD		B,E
		XOR		A
STBFIL1:
		OUT		(98H),A
		DJNZ	STBFIL1
		LD		D,C_KJOF
		JP		FONT_C

PRO_END:
		DS		0D00H
