;-- KANJI EDITOR "FIELD WORK" --

;  File Rutines

; _SAVE("FW-FILE

;--- WORK AREA ---
;		PRINT WORK AREA BACKUP
BK_LIND:	DW		0	;TD_LIN
BK_CAXS:	DW		0	;CUR_AXS

BK_YMAX:	DW		0	;TD_YMAX
BK_EOFS:	DW		0	;TD_EOFS
BK_YNOW:	DW		0	;TD_YNOW

BK_COLF:	DB		0	;FONTCOL
;		FCB
FCB:		DS		37
FCB2:		DS		37
FCBDIR:		DB		0
			DB		'???????????'	;*DM*
			DS		37-12

;--- STRING AREA ---
STRSIZ:		EQU		28-6
M_INLIN:	DB		12,26,C_STAT
			DB		' GOTO:'	;*DM*
			DB		26,71H,0
M_INSTR:	DB		12,26,C_STAT
			DB		'INPUT:'	;*DM*
			DB		26,71H,0
M_STR:		DB		24,7
STR:		DS		STRSIZ+2
			DB		0
M_SAVEQ:	DB		'上書き READY'	;*DM*
			DB		0
M_KILLQ:	DB		'削除しますか'	;*DM*
			DB		0
M_QUES:		DB		12,26,C_EROR,0
M_QUES_:	DB		'？(← YES/NO →)'		;8 MOJI	;*DM*
			DB		0
M_KILLC:	DB		'削除しません'	;*DM*
			DB		EOF
M_KILLM:	DB		'ﾌｧｲﾙ名:'	;*DM*
M_KILLN:	DS		20
			DB		' (EDITｻｲｽﾞ:65535 FILEｻｲｽﾞ:65535 )'	;*DM*
			DB		0
DEL_MTA:	DB		'これはBASICﾌﾟﾛｸﾞﾗﾑです'	;*DM*
			DB		EOF
DEL_MBI:	DB		'先頭:0000H 終了:0000H 開始:0000HこれはBSAVEﾌｧｲﾙです'	;*DM*
			DB		EOF
M_CRLF:		DB		CR,LF,0
M_FILEQ:
			DB		'　∬ ＦIELD ＷORK - MENU ∬'	;*DM*
			DB		CR,LF,CR,LF
			DB		'　　　 (Ｌ): LOAD FILE'	;*DM*
			DB		CR,LF
			DB		'　　　 (Ｓ): SAVE FILE'	;*DM*
			DB		CR,LF
			DB		'　　　 (Ｎ): NAME FILE'	;*DM*
			DB		CR,LF
			DB		'　　　 (Ｄ):  DELETE'	;*DM*
			DB		CR,LF
;			DB		'　　　 (Ｈ):   HELP'	;*DM*
			DB		CR,LF,CR,LF
			DB		'　　 INPUT (L,S,N,D) ? ( )'	;*DM*
			DB		CR,LF,CR,LF
			DB		'　　EDIT FILE :'	;*DM*
M_FILEN:	DB		'FILENAME.DAT'	;*DM*
EOFF:		DB		EOF
M_LOAD:		DB		'  <LOAD FILE>@'	;*DM*
M_SAVE:		DB		'  <SAVE FILE>@'	;*DM*
M_NAME:		DB		'<SET FILE NAME>@'	;*DM*
M_KILL:		DB		' <DELETE FILE>@'	;*DM*

;-- RE-PRINT --
TX_REP:	CALL	CD_CLS
		LD		HL,(BK_LIND)
		CALL	MAKLINE
		LD		A,12
		CALL	LFSET
		LD		DE,(BK_LIND)
		CALL	PRINT
		LD		HL,(BK_CAXS)
		LD		(CUR_AXS),HL
		JP		LFRSET

;-- COLOR --
COLFSAV:
		LD		A,(FONTCOL)
		LD		(BK_COLF),A
		RET
COLFLOD:
		LD		A,(BK_COLF)
		LD		(FONTCOL),A
		RET

;--- FCB BACK UP & RESTORE ---
FCBSAVE:
		LD		HL,FCB
		LD		DE,FCB2
		LD		BC,37
		LDIR
		RET
FCBLOAD:
		LD		HL,FCB2+1
		JP		FNAMSET

;--- FILE NAME to FCB FILE NAME
FNAMSET:
		LD		DE,FCB+1
		LD		B,8
L_FNM:	LD		A,(HL)
		INC		HL
		CP		'.'
		JP		Z,FNM2
		CP		'$'
		JP		Z,FNM1
		CP		' '
		JP		C,FNM1
		LD		(DE),A
		INC		DE
		DJNZ	L_FNM

		LD		A,(HL)
		CP		'.'
		JP		NZ,FILNMK
		INC		HL
		JP		FILNMK

FNM1:	LD		A,B
		ADD		A,3
		LD		B,A
		CALL	FNMSPC
		JP		FNM_END

FNM2:	CALL	FNMSPC
		JP		FILNMK

FNMKR:	LD		DE,FCB+9
FILNMK:	LD		B,3
L_FNMK:	LD		A,(HL)
		INC		HL
		CP		'.'
		JP		Z,FNMKR
		CP		'$'
		JP		Z,_FNMK
		CP		' '
		JP		C,_FNMK
		LD		(DE),A
		INC		DE
		DJNZ	L_FNMK
_FNMK:	CALL	FNMSPC

FNM_END:
		LD		HL,FCB+1
		LD		DE,FILENAM
		LD		BC,8
		LDIR
		LD		A,'.'
		LD		(DE),A
		INC		DE
		LD		BC,3
		LDIR
		JP		JEMODE_

;-- WRITE SPACE
FNMSPC:	LD		A,B
		EX		DE,HL
L_FNMS:	OR		A
		JP		Z,_FNMS
		DEC		A
		LD		(HL),' '
		INC		HL
		JP		L_FNMS
_FNMS:	EX		DE,HL
		RET

;--- MAKE DIRCTORY DATA TO MESSAGE
; INP | (BUF)~: DIRECTORY DATA
;     | DE: WRITE ADDRESS
; OUT | DATA
; USE | ALL

DIRM:	DB		'. () /'	;*DM*

MAKDIRM:
		CALL	PUTM_M
;  (FILE NAME)
		LD		HL,BUF+1
		LD		BC,8
		LDIR
		LD		HL,DIRM
		LDI
		LD		HL,BUF+9
		LD		BC,3
		LDIR
;  (FILE SIZE)
		LD		HL,DIRM+1
		LDI
		LDI
		LD		HL,(BUF+29)
		CALL	PUT_D5
		LD		HL,DIRM+3
		LDI
		LDI
;  (FILE DATE)
;  (NEN)
		LD		HL,1980
		LD		A,(BUF+26)
		SRL		A
		PUSH	AF
		CALL	ADDHLA
		CALL	PUT_D4
		LD		HL,DIRM+5
		LDI
;  (TUKI)
		POP		AF
		LD		A,(BUF+25)
		RRA
		RRA
		RRA
		RRA
		RRA
		AND		15
		LD		L,A
		LD		H,0
		CALL	PUT_D2
		LD		HL,DIRM+5
		LDI
;  (NICHI)
		LD		A,(BUF+25)
		AND		31
		LD		L,A
		LD		H,0
		CALL	PUT_D2
		LD		HL,M_CRLF
		LDI
		LDI
		CALL	PUTM_V
		RET

;--- FILES ---
FILES:	LD		A,1
		CALL	MEMSAVE
		XOR		A
		LD		(JE_MODE),A
		CALL	JEMODE_
		CALL	HS_STOP
		LD		DE,BUF
		CALL	F_DMA
		LD		DE,FCBDIR
		CALL	F_DIR
		LD		DE,TA_HEAD
		JP		NZ,FLSESC

L_FILES:
		CALL	MAKDIRM
		PUSH	DE
		CALL	F_DIR2
		POP		DE
		JR		NZ,_FILES
		JP		L_FILES

_FILES:	
		LD		HL,(TD_YNOW)
		LD		(BK_YNOW),HL
		LD		HL,(TD_EOFS)
		LD		(BK_EOFS),HL
		LD		HL,(TD_YMAX)
		LD		(BK_YMAX),HL
		DEC		DE
		DEC		DE
		LD		A,EOF
		LD		(DE),A
		CALL	LINTES0
		CALL	HS_CONT
		LD		A,C_FSOF
		CALL	KANJI_C
		CALL	TX_HEAD
		CALL	KEYCLR
		CALL	FSELECT
		CP		27
		JP		Z,FLSESC
		CALL	GETYLIN
		EX		DE,HL
		CALL	FNAMSET
FLSEND:	LD		A,1
		CALL	MEMLOAD
		CALL	COLFLOD
		LD		HL,(BK_YMAX)
		LD		(TD_YMAX),HL
		LD		HL,(BK_EOFS)
		LD		(TD_EOFS),HL
		LD		HL,(BK_YNOW)
		LD		(TD_YNOW),HL
		RET
FLSESC:	CALL	FLSEND
		POP		HL
		JP		TX_REP

;--- FILE SELECT ---
FSELECT:
		CALL	FCURON
		CALL	INPUT
		PUSH	AF
		CALL	FCUROF
		POP		AF
		CP		13
		RET		Z
		CP		27
		RET		Z
		LD		HL,FSELECT
		PUSH	HL
		CP		30
		JP		Z,CR__UP
		CP		31
		JP		Z,CR__DW
		RET

;--- FILE CURSOL ---
FCURON:	LD		D,C_KJON
		JP		FSELCUR
FCUROF:	LD		D,C_FSOF
FSELCUR:
		LD		HL,(CUR_AXS)
		LD		A,L
		RLCA
		RLCA
		RLCA
		LD		L,A
		LD		A,(SCRL_L)
		ADD		A,H
		AND		31
		LD		H,A
		LD		E,0F8H
		JP		FONT_C

;--- INPUT FILE NAME ---
INPFILE:
		XOR		A
		LD		(INSTRMD),A
		CALL	INPSTR
		LD		A,(HL)
		CP		EOF
		JP		Z,INPFIL_
		CP		' '
		JP		Z,FILES
		JP		FNAMSET
INPFIL_:
		POP		HL
		JP		TX_REP

;--- INPUT SAVE FILE NAME ---
INSVNAM:
		XOR		A
		LD		(INSTRMD),A
		CALL	INPSTR
		LD		A,(HL)
		CP		EOF
		JP		Z,INSVNA_
		JP		FNAMSET
INSVNA_:
		POP		HL
		JP		TX_REP


;--- LOAD FILE
; IN | HL:FILENAME ADDRESS
; OUT| MEMORY:FILE

TX_LOAD:
		LD		HL,M_LOAD
		CALL	STR_INF
		CALL	INPFILE
		LD		A,1
		CALL	MEMSAVE
		CALL	HS_STOP

		LD		DE,TA_HEAD
		CALL	F_DMA
		LD		DE,FCB
		CALL	F_OPEN
		LD		A,8
		JP		NZ,FIL_ERR
		LD		A,(FCB+19)
		OR		A
		LD		A,13
		JP		NZ,FIL_ERR
		LD		A,(FCB+18)
		OR		A
		LD		A,13
		JP		NZ,FIL_ERR
		LD		DE,(FCB+16)
		LD		HL,TA_SIZE
		OR		A
		SBC		HL,DE
		LD		A,13
		JP		C,FIL_ERR

		LD		DE,FCB
		LD		HL,(FCB+16)
		
		LD		A,H
		OR		L
		JR		Z,TXLOAD_
	; Skip because an error occurs
	; in RDRND when the file size is 0

		CALL	F_READ
		JP		NZ,FIL_ERR
		LD		A,(TA_HEAD)
		CP		0FEH
		JP		Z,FILERRB
		LD		A,12
		JP		NC,FIL_ERR
TXLOAD_:
		CALL	HS_CONT
		LD		HL,(FCB+16)
		LD		(TD_MSIZ),HL
		LD		DE,TA_HEAD
		ADD		HL,DE
		LD		(HL),EOF
		INC		HL
		LD		(HL),EOF
		CALL	LINTES0
		JP		TX_HEAD

;--- DISK ERROR ---
FILERRB:
		CALL	PUTM_M
		LD		DE,BINARYD+4
		LD		HL,(TA_HEAD+1)
		CALL	PUT_N
		LD		DE,BINARYD+13
		LD		HL,(TA_HEAD+3)
		CALL	PUT_N
		LD		DE,BINARYD+22
		LD		HL,(TA_HEAD+5)
		CALL	PUT_N
		CALL	PUTM_V
		LD		A,11
FIL_ERR:
		LD		SP,0	;dummy
		PUSH	AF
		CALL	HS_CONT
		LD		A,1
		CALL	MEMLOAD
		CALL	FCBLOAD
		POP		AF
		CALL	ERROR_W
		JP		TX_REP

;--- DISK HARD ERROR ---
SETDERR:
		LD		HL,(DISKVE)
		LD		(BDISKVE),HL
		LD		HL,DSKERR-2
		LD		(DISKVE),HL
		RET
RESDERR:
		LD		HL,(BDISKVE)
		LD		(DISKVE),HL
		RET
;-- ERROR!! --
BDISKVE:
		DW		0
		DW		DSKERR
DSKERR:	PUSH	BC
		CALL	RAMP1
		POP		BC
		LD		A,C
		SRL		A
		JP		M,DSKER0
		CP		5
		JP		C,DSKER1
		LD		A,C
		ADD		A,20-0AH
		CP		22
		JP		C,DSKER_
		LD		A,22
		JP		DSKER_
DSKER0:	LD		A,-1
DSKER1:	ADD		A,15
DSKER_:	JP		FIL_ERR

;--- SAVE FILE
; IN | --
; OUT| MEMORY:FILE

TX_SAVE:
		LD		HL,M_SAVE
		CALL	STR_INF
		LD		A,1
		CALL	MEMSAVE
TXSAVEM:
		LD		A,(FCB+1)
		CP		32+1
		JR		NC,TXSAVE0
;
		CALL	INSVNAM	;INPUT SAVE FILE NAME
		LD		A,(FCB+1)
		CP		32+1
		LD		A,23
		JP		C,FIL_ERR
TXSAVE0:
		XOR		A
		LD		(JE_MODE),A
		CALL	JEMODE_
		CALL	HS_STOP

		LD		DE,BUF
		CALL	F_DMA
		LD		DE,FCB
		CALL	F_DIR
		JP		NZ,TXSAVE1
;- REWRITE ? -
		CALL	DEL_Q
		LD		HL,M_SAVEQ
		CALL	QUES
		JP		C,TXSAVEC

TXSAVE1:
		CALL	TX_REP
		LD		DE,TA_HEAD
		CALL	F_DMA
		CALL	HS_STOP
		LD		DE,FCB
		CALL	F_MAKE
		LD		A,10
		JP		NZ,FIL_ERR
		LD		HL,(TD_MSIZ)
		LD		DE,FCB
		CALL	F_WRITE
		LD		A,10
		JP		NZ,FIL_ERR
		LD		DE,FCB
		CALL	F_CLOSE
		LD		A,21
		JP		NZ,FIL_ERR

		CALL	HS_CONT
		RET
TXSAVEC:
		LD		A,2
		JP		FIL_ERR

;--- OK ? ---
QUES:	PUSH	HL
		CALL	HS_CONT
		LD		HL,M_QUES
		CALL	STBSET
		POP		HL
		CALL	STBSET
		LD		HL,M_QUES_
		CALL	STBSET
L_QUES:	CALL	INPUT
		CP		29
		JP		Z,E_QUESY
		CP		28
		JP		Z,E_QUESN
		CP		27
		JP		Z,E_QUESN
		JP		L_QUES
E_QUESY:
		CALL	JEMODE_
		OR		A
		RET
E_QUESN:
		CALL	JEMODE_
		SCF
		RET

;--- FILE MODE SELECT ---
TX_FILE:
		CALL	SETDERR
		LD		HL,RESDERR
		PUSH	HL
		LD		(FIL_ERR+1),SP
		LD		HL,(TD_LIN)
		LD		(BK_LIND),HL
		LD		HL,(CUR_AXS)
		LD		(BK_CAXS),HL

		CALL	FCBSAVE
		CALL	CLR_INF
		CALL	COLFSAV

		LD		HL,FILENAM
		LD		DE,M_FILEN
		LD		BC,12
		LDIR

		CALL	CD_CLS
		LD		A,C_MENU
		LD		(SPCFLG),A
		CALL	KANJI_C
		LD		DE,M_FILEQ
		CALL	PRINT
		XOR		A
		LD		(SPCFLG),A
		LD		HL,1018H
		LD		(PR_AXS),HL
		LD		A,C_EROR
		CALL	KANJI_C

L_FILE:	CALL	INPUT2
		CALL	KANJI_F
		JP		C,L_FILE2
L_FILE1:
		PUSH	AF
		CALL	KANJI_S+1
		POP		AF
L_FILE2:
		LD		HL,TX_LOAD
		CP		'L'
		JP		Z,E_FILE
		LD		HL,TX_SAVE
		CP		'S'
		JP		Z,E_FILE
		LD		HL,TX_NAME
		CP		'N'
		JP		Z,E_FILE
		LD		HL,TX_KILL
		CP		'D'
		JP		Z,E_FILE
;		LD		HL,TX_HELP
;		CP		'H'
;		JP		Z,E_FILE
		LD		HL,CSLFILE
;		CP		13
;		JP		Z,E_FILE
		CP		27
		JP		Z,E_FILE
		JP		L_FILE
E_FILE:	PUSH	HL
		CALL	COLFLOD
		POP		HL
		JP		(HL)

CSLFILE:
		CALL	TX_REP
		RET

;--- FILE NAME ---
TX_NAME:
		LD		HL,M_NAME
		CALL	STR_INF
		CALL	INPFILE
		JP		TX_REP

;--- FILE DELETE ---

TX_KILL:
		LD		HL,M_KILL
		CALL	STR_INF
		CALL	KILL_L
		CALL	FCBLOAD
		RET

KILL_L:	CALL	FCBLOAD
		CALL	JEMODE_
		CALL	FILES
		CALL	DEL_Q
		LD		HL,M_KILLQ
		CALL	QUES
		JP		NC,KILL_F
		CALL	FCBLOAD
		CALL	JEMODE_
		CALL	CD_CLS
		LD		A,C_EROR
		CALL	KANJI_C
		LD		DE,M_KILLC
		CALL	PRINT
		CALL	COLFLOD
		JP		KILL_L

KILL_F:	CALL	HS_STOP
		LD		DE,FCB
		CALL	F_DEL
		LD		A,8
		JP		NZ,FIL_ERR
		CALL	HS_CONT
		JP		KILL_L

;--- DELETE OK? ---
DEL_Q:	LD		A,1
		CALL	MEMSAVE

		LD		DE,TA_HEAD
		CALL	F_DMA
		CALL	HS_STOP
		LD		DE,FCB
		CALL	F_OPEN
		LD		DE,FCB
		LD		HL,200H
		CALL	F_READ
		CALL	HS_CONT

		CALL	CD_CLS
		LD		A,C_STAT
		LD		(SPCFLG),A
		CALL	KANJI_C
		LD		HL,FILENAM
		LD		DE,M_KILLN
		LD		BC,12
		LDIR
		CALL	PUTM_M
		LD		DE,M_KILLN+31
		LD		HL,(TD_MSIZ)
		CALL	PUT_D5
		LD		DE,M_KILLN+46
		LD		HL,(FCB+16)
		CALL	PUT_D5
		CALL	PUTM_V
		LD		DE,M_KILLM
		CALL	PRINT
		XOR		A
		LD		(SPCFLG),A
		LD		A,C_MJOF
		CALL	KANJI_C

		LD		DE,TA_HEAD
		LD		A,10+1
		CALL	LFSET
		CALL	PRINT
		CALL	LFRSET
		CALL	COLFLOD
		LD		A,(TA_HEAD)
		CP		0FEH
		JP		Z,DEL_QBI
		JP		NC,DEL_QTA
		LD		A,1
		JP		MEMLOAD
DEL_QTA:
		LD		H,UD_SCR
		LD		DE,DEL_MTA
		JP		DEL_MP
DEL_QBI:
		CALL	PUTM_M
		LD		DE,DEL_MBI+5
		LD		HL,(TA_HEAD+1)
		CALL	PUT_N
		LD		DE,DEL_MBI+16
		LD		HL,(TA_HEAD+3)
		CALL	PUT_N
		LD		DE,DEL_MBI+27
		LD		HL,(TA_HEAD+5)
		CALL	PUT_N
		CALL	PUTM_V
		LD		H,UD_SCR-2
		LD		DE,DEL_MBI
DEL_MP:	LD		L,0
		LD		(PR_AXS),HL
		LD		A,C_EROR
		CALL	KANJI_C
		CALL	PRINT
		CALL	COLFLOD
		LD		A,1
		JP		MEMLOAD
