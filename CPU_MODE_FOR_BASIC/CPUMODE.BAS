10 CLEAR100,&HD000:DEFINTA-Z:DIM M$(2):AD=&HD000
20 I=PEEK(&H2D):IF I>2 THEN 40
30 PRINT"CHGCPU requires Turbo R or higher.":END
40 DEFUSR1=AD:RESTORE
50 READ A$:IF A$<>"*"THEN POKE AD,VAL("&H"+A$):AD=AD+1:GOTO 50
60 DATA FE,02,C0,E5,0E,6F,CD,7D,F3,B7,3E,00,20,01,78,DD,E1,DD,77,02,DD,36,03,00,C9,*
70 V=USR1(0):PRINT"DISK ROM:MSX-DOS";HEX$(V-(V<1))
80 IF V<2 THEN PRINT"[CAUTION] MSX-DOS1 is not support R800."
90 DEFUSR=AD
100 READ A$:IF A$<>"*"THEN POKE AD,VAL("&H"+A$):AD=AD+1:GOTO 100
110 DATA FE,02,C0,E5,DD,E1,3A,83,01,B7,28,0F,DD,7E,02,FE,04,30,05,F6,80,CD,80,01,CD,83,01,DD,77,02,DD,36,03,00,C9,*
120 FOR I=0 TO 2:READ M$(I):NEXT
130 DATA "Z80","R800(ROM)","R800(DRAM)"
140 I=USR(255):PRINT"CPU MODE:";M$(I):PRINT
150 PRINT "[ ";:FOR I=0 TO 2:PRINT HEX$(I);"=";M$(I);" ";:NEXT:PRINT"]"
160 PRINT"NEW CPU MODE(0-2)?";
170 A$=INPUT$(1):IF(A$<"0")OR(A$>"2")THEN BEEP:GOTO 170
180 I=VAL(A$):A=USR(I):PRINT HEX$(I);"=";M$(I):PRINT
190 PRINT"CHANGED to CPU MODE:";M$(A)
