1000 CLEAR1500,&HB000:DEFINTA-Z
1010 UA=&HC018:BLOAD"SRX2.BIN"
1020 DEFUSR9=UA+&H0'U=USR9(1)'START BGM DRIVER
1030               'U=USR9(0)'END BGM DRIVER
1040 DEFUSR1=UA+&H3'U=USR1(&HC000)'PLAY BGM (&HC000=BGM DATA)
1050 DEFUSR2=UA+&H6'U=USR2(0)'STOP BGM
1060 DEFUSR3=UA+&H9'U=USR3(&HC800)'PLAY SE (&HC800=SE DATA)
1070 DEFUSR4=UA+&HC'U=USR4(1)'FADE OUT (WAIT=1)
1080 DEFUSR5=UA+&HF'U=USR5(0)'IF U<>0 PLAYING NOW
1090 '
1100 A$="U=USR9(0)":KEY1,A$+CHR$(13)
1110 PRINT"'KEY1,"+CHR$(34)+A$+CHR$(34)+"'END BGM DRIVER":PRINT
1120 ON ERROR GOTO 1290:ON STOP GOSUB 1290:STOP ON
1130 U=USR9(1):PRINT"U=USR9(1)'START BGM DRIVER":PRINT
1140 '
1150 LA=&HB000:BLOAD"BGM02.BIN",LA:U=USR1(LA)'PLAY BGM
1160 '
1170 A=&HB800:S1=A:GOSUB 2010:S2=A:GOSUB 2010'SET SE
1180 U=USR3(S1):PRINT"U=USR3(S1)'play SE1":PRINT
1190 TIME=0:FOR I=0 TO 1:I=-(TIME>60):NEXT
1200 U=USR3(S2):PRINT"U=USR3(S2)'play SE2":PRINT
1210 '
1220 PRINT "SPACE=SE1 / ENTER=SE2 / OTHER=END"
1230 I$=INPUT$(1)
1240 IF I$=CHR$(32) THEN U=USR3(S1):GOTO 1230
1250 IF I$=CHR$(13) THEN U=USR3(S2):GOTO 1230
1260 U=USR4(4)'FADE OUT (WAIT=4)
1270 U=USR5(0):IF U THEN 1270
1280 '
1290 U=USR9(0)'TERMINATE BGM DIRVER
1300 ON ERROR GOTO 0:STOP OFF:END
1310 '
2000 'LOAD SE
2010 READ D:POKE A,D:A=A+1 'PRIORITY
2020 READ A$,D
2030 IF A$="FREQ" THEN POKE A,0:POKE A+1,D AND 255:POKE A+2,PEEK(VARPTR(D)+1):A=A+3:GOTO 2020
2040 IF A$="VOL"  THEN POKE A,1:POKE A+1,D:A=A+2:GOTO 2020
2050 IF A$="NOISE"THEN POKE A,2:POKE A+1,D:A=A+2:GOTO 2020
2060 IF A$="WAIT" THEN POKE A,3:POKE A+1,D:A=A+2:GOTO 2020
2070 IF A$="END"  THEN POKE A,4:A=A+1:RETURN
2080 ERROR 0:POKE A,4:A=A+1:RETURN
2090 '
2100 'SE1
2110 DATA 32 :'PRIORITY
2120 DATA "FREQ",&H8000 :'32768=TONE OFF
2130 DATA "VOL",13,"NOISE",&H80,"WAIT",0
2140 DATA "VOL",12,"NOISE",&H90,"WAIT",0
2150 DATA "VOL",11,"NOISE",&H91,"WAIT",0
2160 DATA "VOL",10,"NOISE",&H92,"WAIT",0
2170 DATA "VOL",9, "NOISE",&H93,"WAIT",0
2180 DATA "VOL",7, "NOISE",&H8A,"WAIT",0
2190 DATA "VOL",6, "NOISE",&H8C,"WAIT",0
2200 DATA "VOL",5, "NOISE",&H8F,"WAIT",0
2210 DATA "VOL",4, "NOISE",&H8C,"WAIT",0
2220 DATA "VOL",3, "NOISE",&H8A,"WAIT",0
2230 DATA "VOL",2, "NOISE",&H88,"WAIT",0
2240 DATA "VOL",1, "NOISE",&H84,"WAIT",0
2250 DATA "END",0
2300 'SE2
2310 DATA 64 :'PRIORITY
2320 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
2330 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2340 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
2350 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2360 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
2370 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2380 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
2390 DATA "VOL",9 ,"FREQ",&H6080,"WAIT",1
2400 DATA "VOL",8 ,"FREQ",&H6070,"WAIT",1
2410 DATA "VOL",7 ,"FREQ",&H6060,"WAIT",1
2420 DATA "VOL",6 ,"FREQ",&H6050,"WAIT",1
2430 DATA "VOL",5 ,"FREQ",&H6040,"WAIT",1
2440 DATA "VOL",4 ,"FREQ",&H6030,"WAIT",1
2450 DATA "VOL",3 ,"FREQ",&H6020,"WAIT",1
2460 DATA "VOL",2 ,"FREQ",&H6010,"WAIT",1
2470 DATA "VOL",1 ,"FREQ",&H6000,"WAIT",1
2480 DATA "END",0

