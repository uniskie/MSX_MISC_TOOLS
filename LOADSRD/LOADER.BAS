1000 ON ERROR GOTO 1010:_ANK:GOTO 1020
1010 RESUME NEXT
1020 ON ERROR GOTO 0
1030 '
1040 COLOR15,0,0:LOCATE,,0:SCREEN,,,,,0:VDP(9)=VDP(9)AND253
1050 CLEAR200,&HB000:DEFINT A-Z:DIM P(255)
1060 P=1 'Û°ÄÞ»·Íß°¼Þ
1070 SD=7 'ÃÞÌ«ÙÄ½¸Ø°ÝÓ°ÄÞ
1080 ED$="SC" 'ÃÞÌ«ÙÄ¶¸Á®³¼
1090 ID=0 'ÃÞÌ«ÙÄ²ÝÀ°Ú°½Ó°ÄÞ
1100 PE=0 'ÊßÚ¯Ä ¶Þ Õ³º³
1110 PS$="" 'ÊßÚ¯Ä ¦ ¾°ÌÞ¼À
1120 B0$="" 'BSAVE¼À
1130 B1$="" 'BSAVE¼À
1140 VP=1 'ÊßÚ¯Ä:VDPÀÞ²Ú¸Ä¿³»(VRAMÆ¶·ºÏÅ²)
1150 '
1160 '---- ¼®·¶ ----
1170 BLOAD"LOADSR.BOF"
1180 DEFUSR=&HD000 'READ IMAGE FILE
1190 DEFUSR1=&HD003 'SET_PAL
1200 DEFUSR9=&H156 '·°ÊÞ¯Ì§¸Ø±
1210 PLAY"T120SM200L64"
1220 '
1230 ON STOP GOSUB 2290:STOP ON:ON ERROR GOTO 2090
1240 '
1250 '--SELECT FILE--
1260 FILES:PRINT"":GOSUB 1390:LINEINPUT"file=";A$:IF A$="" THEN 1250
1270 E$="":GOSUB 1500 'A$=µµÓ¼Þ
1280 '
1290 '--- ¶¸Á®³¼ ÄØÀÞ¼ ---
1300 I=INSTR(A$,"."):IF I=0 THEN 1320
1310 E$=LEFT$(MID$(A$,I+1),3):A$=LEFT$(A$,I-1) ' .É²ÁÃÞÌÞÝØ
1320 IF E$="" THEN E$=MID$(A$,9) ' .¶ÞÅ¹ÚÊÞ 9Ó¼ÞÒ²º³¶Þ ¶¸Á®³¼
1330 F$=LEFT$(A$+SPACE$(8),8):GOSUB 1540 'µµÓ¼ÞÆ¼Ã¤¶¸Á®³¼ ¹Ý»
1340 '
1350 '--LOAD & DISPLAY--
1360 GOSUB 1660
1370 SCREEN 0,,,,,0:GOTO 1250 'ÓÄÞÙ
1380 '
1390 '--INFO--
1400 IF LEN(PS$) THEN PRINT">> "+CHR$(34)+PS$+CHR$(34)+" ¦ SAVE ¼Ï¼À"
1410 IF LEN(B0$) THEN PRINT">> "+CHR$(34)+B0$+CHR$(34)+" ¦ BSAVE ¼Ï¼À"
1420 IF LEN(B1$) THEN PRINT">> "+CHR$(34)+B1$+CHR$(34)+" ¦ BSAVE ¼Ï¼À"
1430 PRINT"-- ¶Þ¿Þ³ Ë®³¼ÞÁ­³ ¿³» --"
1440 PRINT" »Õ³:ÊßÚ¯ÄBANK·Ø¶´"
1450 PRINT" P  :ÊßÚ¯ÄÌ§²Ù¾°ÌÞ(*.PL?)"
1460 PRINT" B  :BSAVE(*.SC?)(ÊßÚ¯ÄÌ¸Ñ)"
1470 'IF PE THEN PRINT"COPY P TO "+CHR$(34)+F$+".PL"+HEX$(SC)+CHR$(34)+" 'ÊßÚ¯Ä¾°ÌÞ"
1480 PS$="":B0$="":B1$="":RETURN
1490 '
1500 '--LOWER CASE LETTER TO UPPER CASE--
1510 IF LEN(A$)<1 THEN RETURN
1520 FORI=1TOLEN(A$):A=ASC(MID$(A$,I,1)):A=A+((A>96)AND(A<123))*32:MID$(A$,I,1)=CHR$(A):NEXT:RETURN
1530 '
1540 '--CHECK EXTENSION--
1550 RESTORE
1560 READ A$,I,L,K:IF A$=E$ THEN 1580
1570 IF A$<>"" THEN 1560 ELSE 1590
1580 IF A$<>"" THEN SC=I:IL=L:XP=K:EX$=A$:RETURN
1590 SC=SD:IL=ID:EX$="":RETURN'ED$+HEX$(SC):RETURN
1600 ' "EXT.", SCEEN MODE, INTERLACE, USE PLT FILE
1610 DATA"SR5",5,0,1,"SC5",5,0,0,"SR7",7,0,1,"SC7",7,0,0,"SR8",8,0,0,"SC8",8,0,0,"SRA",10,0,0,"S10",10,0,0,"SRC",12,0,0,"S12",12,0,0,"SRS",12,0,0
1620 DATA"S50",5,3,0,"S51",5,3,0,"S70",7,3,0,"S71",7,3,0,"S80",8,3,0,"S81",8,3,0,"SA0",10,3,0,"SA1",10,3,0,"SC0",12,3,0,"SC1",12,3,0
1630 DATA"R50",5,3,1,"R51",5,3,1,"R70",7,3,1,"R71",7,3,1,"R80",8,3,0,"R81",8,3,0,"RA0",10,3,0,"RA1",10,3,0,"RC0",12,3,0,"RC1",12,3,0
1640 DATA "",0,0,0
1650 '
1660 '--LOAD SCREEN--
1670 SCREEN SC,,,,,IL:PE=0:PS$="":VL=0:VDP(9)=VDP(9) OR 2 'sprite off
1680 IF IL>0 THEN SETPAGE 1,0:FL$=F$+LEFT$(EX$,2)+"0":A$=USR(FL$):FL$=""
1690 IF IL>0 THEN SETPAGE 1,1:FL$=F$+LEFT$(EX$,2)+"1":A$=USR(FL$):FL$=""
1700 IF IL=0 THEN SETPAGE P,P:FL$=F$+EX$:A$=USR(FL$):FL$=""
1710 '
1720 ON 1+XP GOSUB 1950,1910 'READ PALETTE
1730 GOSUB 1850 ' SET PALETTE
1740 '
1750 '--DISPLAY--
1760 A$=INPUT$(1):GOSUB 1500:A=ASC(A$)
1770 B=PN:PN=(PN+(A=29)-(A=28))AND 7:IF B<>PN THEN PLAY"O4AD":GOTO 1730  'ÊßÚ¯ÄÁªÝ¼Þ
1780 B=VL:VL=(VL+(A=30)-(A=31))AND 255:IF B<>VL THEN VDP(24)=VL:GOTO 1760  'ÊßÚ¯ÄÁªÝ¼Þ
1790 IF(A$="B")AND PE THEN GOSUB 2010 'BSAVE
1800 IF(A$="P")AND PE THEN FL$=F$+".PL"+HEX$(SC):COPY P TO FL$:PS$=FL$:FL$="":PLAY"O5CGC" 'ÊßÚ¯Ä¾°ÌÞ
1810 IF A=32 THEN P=(P+1)AND(1-(SC<7)*2):SETPAGEP:GOTO1760
1820 VDP(9)=VDP(9) AND 253 'sprite on
1830 RETURN
1840 '
1850 '--SET PALETTE--
1860 IF SC>7 THEN RETURN
1870 IF VP THEN 1890
1880 J=PN*16:FORI=0 TO 15:COLOR=(I,(P(J)\16)AND15,P(J)\256,P(J)AND15):J=J+1:NEXT:RETURN
1890 J=PN*16:A=USR1(VARPTR(P(PN*16))):RETURN'VDP DIRECT ACCESS(VRAMÆ ·Û¸»ÚÅ²)
1900 '
1910 '--LOAD PALETTE FILE--
1920 FL$=F$+".PL"+HEX$(SC):COPY FL$ TO P:FL$=""
1930 PE=1:RETURN
1940 '
1950 '-- READ VRAM PALETTE TABLE--
1960 VA=VAL("&H"+MID$("0400,0F00,2020,1B80,2020,1B80,7680,7680,FA80,FA80,7680,FA80,FA80,FA80",(SC+1)*5+1,4))
1970 RA=VARPTR(P(0)):FOR I=0 TO 31:POKE RA+I,VPEEK(VA+I):NEXT 'PAL SLOT 0
1980 FOR I=1 TO 15:FOR J=0 TO15:P(I*16+J)=P(J):NEXT 'PAL SLOT 1-15
1990 PE=1:RETURN
2000 '
2010 '-- WRITE PALETTE TO VRAM, and BSAVE --
2020 IF PE THEN BP=VP:VP=0:GOSUB 1850:VP=BP 'ÊßÚ¯Ä¦VRAMÆ¶·ºÑ
2030 EA=VAL("&H"+MID$("0FFF,18FF,37FF,37FF,203F,37FF,769F,769F,FA9F,FA9F,769F,FA9F,FA9F,FA9F",(SC+1)*5+1,4))
2040 IF IL>0 THEN SETPAGE 1,0:FL$=F$+".S"+HEX$(SC)+"0":BSAVE FL$,0,EA,S:B0$=LF$
2050 IF IL>0 THEN SETPAGE 1,1:FL$=F$+".S"+HEX$(SC)+"1":BSAVE FL$,0,EA,S:B1$=FL$
2060 IF IL=0 THEN SETPAGE P,P:FL$=F$+".SC"+HEX$(SC):BSAVE FL$,0,EA,S:B0$=FL$
2070 PLAY"CCG":FL$="":BS=1:RETURN
2080 '
2090 '--TRAP ERROR--
2100 ER$=""
2110 IF ERR=19 THEN ER$="Device I/O error"
2120 IF ERR=53 THEN ER$="File not found"
2130 IF ERR=60 THEN ER$="Bad FAT"
2140 IF ERR=66 THEN ER$="Disk full"
2150 IF ERR=67 THEN ER$="Too many files"
2160 IF ERR=68 THEN ER$="Disk write protected"
2170 IF ERR=69 THEN ER$="Disk I/O error"
2180 IF ERR=70 THEN ER$="Disk offline"
2190 IF ERR=254 THEN ER$="File not supported type"
2200 IF ERR=255 THEN ER$="Type miss match in USR call"
2210 IF ER$="" THEN 2270 '´×°Ã²¼»¾Ù
2220 IF PEEK(&HFCAF)>1 THEN SCREEN0
2230 BEEP:COLOR 9:PRINT:PRINT"** ";ER$;" in";ERL; " **"
2240 IF LEN(FL$) THEN PRINT " -> ";CHR$(34)+FL$+CHR$(34)
2250 PRINT"- PUSH ANY KEY -"
2260 A=USR9(0):A$=INPUT$(1):COLOR15:A=USR9(0):RESUME 1250
2270 ON ERROR GOTO 0 'RESUME¾½ÞÆERRORÊ¯¾²»¾Ã END
2280 '
2290 '--END--
2300 ON ERROR GOTO 0
2310 VDP(9)=VDP(9) AND 253 'sprite on
