1000 ON ERROR GOTO 1010:_ANK:GOTO 1020
1010 RESUME NEXT
1020 ON ERROR GOTO 0::PLAY"T120SM200L64"
1030 '
1040 COLOR15,0,0:LOCATE,,0:SCREEN,,,,,0:VDP(9)=VDP(9)AND253
1050 CLEAR200,&HB000:DEFINT A-Z:DIM P(255)
1060 P=1 'Û°ÄÞ»·Íß°¼Þ
1070 SD=7 'ÃÞÌ«ÙÄ½¸Ø°ÝÓ°ÄÞ
1080 ED$="SC" 'ÃÞÌ«ÙÄ¶¸Á®³¼
1090 ID=0 'ÃÞÌ«ÙÄ²ÝÀ°Ú°½Ó°ÄÞ
1100 PE=0 'ÊßÚ¯Ä ¶Þ Õ³º³
1110 PS=0 'ÊßÚ¯Ä ¦ ¾°ÌÞ¼À
1120 B0$="" 'BSAVE¼À
1130 B1$="" 'BSAVE¼À
1140 'VP=0 'ÊßÚ¯Ä:VRAMÊßÚ¯ÄÃ°ÌÞÙ = VRAM¶Þ¶·¶ÜÙ
1150 '
1160 '---- ¼®·¶ ----
1170 BLOAD"LOADSRD.BOF"
1180 DEFUSR=&HD000 'READ IMAGE FILE
1190 DEFUSR1=&HD003 'SET_PAL
1200 '
1210 FILES:PRINT"":GOSUB 1340:LINEINPUT"file=";A$:IF A$="" THEN 1210
1220 E$="":GOSUB 1450 'A$=µµÓ¼Þ
1230 '
1240 '--- ¶¸Á®³¼ ÄØÀÞ¼ ---
1250 I=INSTR(A$,"."):IF I=0 THEN 1270
1260 E$=LEFT$(MID$(A$,I+1),3):A$=LEFT$(A$,I-1) ' .É²ÁÃÞÌÞÝØ
1270 IF E$="" THEN E$=MID$(A$,9) ' .¶ÞÅ¹ÚÊÞ 9Ó¼ÞÒ²º³¶Þ ¶¸Á®³¼
1280 F$=LEFT$(A$+SPACE$(8),8):GOSUB 1490 'µµÓ¼ÞÆ¼Ã¤¶¸Á®³¼ ¹Ý»
1290 '
1300 '--DISPLAY--
1310 GOSUB 1610
1320 SCREEN 0,,,,,0:GOTO 1210 'ÓÄÞÙ
1330 '
1340 '--INFO--
1350 IF PS THEN PRINT">> "+CHR$(34)+F$+".PL"+HEX$(SC)+CHR$(34)+" ¦ SAVE ¼Ï¼À"
1360 IF LEN(B0$) THEN PRINT">> "+CHR$(34)+B0$+CHR$(34)+" ¦ BSAVE ¼Ï¼À"
1370 IF LEN(B1$) THEN PRINT">> "+CHR$(34)+B1$+CHR$(34)+" ¦ BSAVE ¼Ï¼À"
1380 PRINT"-- ¶Þ¿Þ³ Ë®³¼ÞÁ­³ ¿³» --"
1390 PRINT" »Õ³:ÊßÚ¯ÄBANK·Ø¶´"
1400 PRINT" P  :ÊßÚ¯ÄÌ§²Ù¾°ÌÞ(*.PL?)"
1410 PRINT" B  :BSAVE(*.SC?)(ÊßÚ¯ÄÌ¸Ñ)"
1420 'IF PE THEN PRINT"COPY P TO "+CHR$(34)+F$+".PL"+HEX$(SC)+CHR$(34)+" 'ÊßÚ¯Ä¾°ÌÞ"
1430 PS=0:B0$="":B1$="":RETURN
1440 '
1450 '--LOWER CASE LETTER TO UPPER CASE--
1460 IF LEN(A$)<1 THEN RETURN
1470 FORI=1TOLEN(A$):A=ASC(MID$(A$,I,1)):A=A+((A>96)AND(A<123))*32:MID$(A$,I,1)=CHR$(A):NEXT:RETURN
1480 '
1490 '--CHECK EXTENSION--
1500 RESTORE
1510 READ A$,I,L,K:IF A$=E$ THEN 1530
1520 IF A$<>"" THEN 1510 ELSE 1540
1530 IF A$<>"" THEN SC=I:IL=L:XP=K:EX$=A$:RETURN
1540 SC=SD:IL=ID:EX$=ED$+HEX$(SC):RETURN
1550 ' "EXT.", SCEEN MODE, INTERLACE, USE PLT FILE
1560 DATA"SR5",5,0,1,"SC5",5,0,0,"SR7",7,0,1,"SC7",7,0,0,"SR8",8,0,0,"SC8",8,0,0,"SRA",10,0,0,"S10",10,0,0,"SRC",12,0,0,"S12",12,0,0,"SRS",12,0,0
1570 DATA"S50",5,3,0,"S51",5,3,0,"S70",7,3,0,"S71",7,3,0,"S80",8,3,0,"S81",8,3,0,"SA0",10,3,0,"SA1",10,3,0,"SC0",12,3,0,"SC1",12,3,0
1580 DATA"R50",5,3,1,"R51",5,3,1,"R70",7,3,1,"R71",7,3,1,"R80",8,3,0,"R81",8,3,0,"RA0",10,3,0,"RA1",10,3,0,"RC0",12,3,0,"RC1",12,3,0
1590 DATA "",0,0,0
1600 '
1610 '--LOAD SCREEN--
1620 SCREEN SC,,,,,IL:PE=0:PS=0
1630 IF IL>0 THEN SETPAGE 1,0:A$=USR(F$+LEFT$(EX$,2)+"0")
1640 IF IL>0 THEN SETPAGE 1,1:A$=USR(F$+LEFT$(EX$,2)+"1")
1650 IF IL=0 THEN SETPAGE P,P:A$=USR(F$+EX$)
1660 '
1670 ON 1+XP GOSUB 1850,1810 'READ PALETTE
1680 GOSUB 1760 ' SET PALETTE
1690 '
1700 A$=INPUT$(1):GOSUB 1450:A=ASC(A$)
1710 PB=PN:PN=(PN+(A=29)-(A=28))AND 7:IF PB<>PN THEN PLAY"O4AD":GOTO 1680  'ÊßÚ¯ÄÁªÝ¼Þ
1720 IF(A$="B")AND PE THEN GOSUB 1910 'VRAMÊßÚ¯Ä¶·ºÐ & BSAVE
1730 IF(A$="P")AND PE THEN COPY P TO F$+".PL"+HEX$(SC):PS=1:PLAY"O5CGC" 'ÊßÚ¯Ä¾°ÌÞ
1740 RETURN
1750 '
1760 '--SET PALETTE--
1770 IF VP THEN 1790
1780 J=PN*16:FORI=0 TO 15:COLOR=(I,(P(J)\16)AND15,P(J)\256,P(J)AND15):J=J+1:NEXT:RETURN
1790 J=PN*16:A=USR1(VARPTR(P(PN*16))):RETURN'VDP DIRECT ACCESS(VRAMÆ ·Û¸»ÚÅ²)
1800 '
1810 '--LOAD PALETTE FILE--
1820 COPY F$+".PL"+HEX$(SC) TO P
1830 PE=1:RETURN
1840 '
1850 '-- READ VRAM PALETTE TABLE--
1860 VA=VAL("&H"+MID$("0400,0F00,2020,1B80,2020,1B80,7680,7680,FA80,FA80,7680,FA80,FA80,FA80",(SC+1)*5+1,4))
1870 RA=VARPTR(P(0)):FOR I=0 TO 31:POKE RA+I,VPEEK(VA+I):NEXT 'PAL SLOT 0
1880 FOR I=1 TO 15:FOR J=0 TO15:P(I*16+J)=P(J):NEXT 'PAL SLOT 1-15
1890 PE=1:RETURN
1900 '
1910 '-- WRITE PALETTE TO VRAM, and BSAVE --
1920 IF PE THEN BP=VP:VP=0:GOSUB1760:VP=BP 'ÊßÚ¯Ä¦VRAMÆ¶·ºÑ
1930 EA=VAL("&H"+MID$("0FFF,18FF,37FF,37FF,203F,37FF,769F,769F,FA9F,FA9F,769F,FA9F,FA9F,FA9F",(SC+1)*5+1,4))
1940 IF IL>0 THEN SETPAGE 1,0:B0$=F$+".S"+HEX$(SC)+"0":BSAVE B0$,0,EA,S
1950 IF IL>0 THEN SETPAGE 1,1:B1$=F$+".S"+HEX$(SC)+"1":BSAVE B1$,0,EA,S
1960 IF IL=0 THEN SETPAGE P,P:B0$=F$+".SC"+HEX$(SC):BSAVE B0$,0,EA,S
1970 PLAY"CCG":BS=1:RETURN
