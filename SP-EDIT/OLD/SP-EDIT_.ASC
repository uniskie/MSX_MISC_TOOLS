1 '***********************************
2 '* SPRITE EDITOR forSCREEN5 ver2.2 *
3 '*(C)UNISKIE SARBUNRO(UnSoft) 1992'*
4 '***********************************
5 CLS:LOCATE,,0:PRINT"Sprite Editor for SCREEN5":DEFUSR9=&H156:GOTO78
6 '**** SUB RUTINES ****
7 '** SPRITE PT **
8 LOCATE 17,18:PRINT"PAGE:";HEX$(PG);"-";HEX$(PT)
9 FORI=0TO3:VPOKE&H1B04+I*4,159:VPOKE&H1B06+I*4,(PG*4+I)*4:NEXT
10 LOCATE 0,23:PRINT SPC(20);
11 VPOKE&H1B04+PT*4,151
12 IF CO THEN RETURN
13 ' PRINT PT
14 VPOKE &H1B02,(PG*4+PT)*4
15 LOCATE20,2:PRINT"No."+RIGHT$("0"+HEX$(PG*4+PT),2)
16 A=GA*256+(PG*4+PT)*32
17 A=USR(A):RETURN
18 '** END **
19 ONERRORGOTO0:POKE&HF3C6,GS:SCREEN0,,1:END
20 '** LOAD DISK **
21 LOCATE19,16:PRINT"LOAD(0-7)?";
22 A$=INPUT$(1)
23 IF A$=CHR$(13) THEN 27
24 IF A$<"0" OR A$>"7" THEN GOSUB62:GOTO 22
25 PRINTA$:BLOAD"SPRITE"+A$+".SC5",S
26 LOCATE19,16:PRINT SPC(11):RETURN 121
27 GOSUB62:LOCATE19,16:PRINT SPC(11):RETURN
28 '** SAVE DISK **
29 LOCATE19,16:PRINT"SAVE(0-7)?";
30 A$=INPUT$(1)
31 IF A$=CHR$(13) THEN 27
32 IF A$<"0" OR A$>"7" THEN GOSUB62:GOTO 30
33 PRINTA$:BSAVE"SPRITE"+A$+".SC5",GA*256,GA*256+32*64-1,S
34 LOCATE19,16:PRINT SPC(11):RETURN
35 '** COPY **
36 LOCATE19,16:PRINT"COPY   No.":CO=1:GOSUB 52:CO=0
37 IF E THEN 40
38 FORI=0TO31:VPOKE GA*256+(PG*4+PT)*32+I,VPEEK(GA*256+(SG*4+ST)*32+I):NEXT
39 PG=SG:PT=ST:GOSUB7
40 LOCATE18,16:PRINTSPC(14):RETURN 122
41 '** SELECT **
42 LOCATE19,16:PRINT"SELECT No.":GOSUB 52
43 GOSUB7:LOCATE18,16:PRINTSPC(14)
44 IF E THEN RETURN 122 ELSE RETURN 122
45 '** CLS **
46 LOCATE21,16:PRINT"CLS?";
47 A$=INPUT$(1):IF A$=CHR$(13) THEN GOSUB 62:GOTO 51
48 IF A$<>" " THEN GOSUB 62:GOTO 47
49 FORI=0TO31:VPOKEGA*256+(PG*4+PT)*32+I,0:NEXT
50 LOCATE21,16:PRINTSPC(4):RETURN 121
51 LOCATE21,16:PRINTSPC(4):RETURN
52 '**SELECT SUB **
53 SG=PG:ST=PT:E=0:LOCATE 1+PT*4,23:PRINT"(??)";
54 A$=INKEY$:IF A$=CHR$(13) OR(FNAAND4)=0 THEN GOSUB62:PG=SG:PT=ST:E=1:RETURN
55 LOCATE 29,16:PRINT RIGHT$("0"+HEX$(PG*4+PT),2)
56 IF STRIG(0)OR STRIG(1) THEN RETURN
57 S=STICK(0)OR STICK(1):IF S=0 THEN 54
58 PG=(PG+(S=5)-(S=1))AND15
59 PT=(PT+(S=7)-(S=3))AND3
60 GOSUB 7:LOCATE 1+PT*4,23:PRINT"(??)";
61 GOTO54
62 '** ERROR FLASH **
63 COLOR=(0,7,7,7):COLOR=(0,0,0,0):RETURN
64 '** DISK ERROR **
65 ER=ERR:EL=ERL:A$=""
66 GOSUB62
67 IF ER=53 THEN A$="File not found"
68 IF ER=68 THEN A$="Write protect "
69 IF ER=70 THEN A$=" Disk offline "
70 IF ER=69 THEN A$="Disk I/O error"
71 IF ER=67 THEN A$="File too Full "
72 IF ER=60 THEN A$="   Bad fat    "
73 IF A$="" THEN SCREEN0:ON ERROR GOTO 0:END
74 RESUME 75
75 LOCATE18,16:PRINT A$
76 IF STRIG(0) OR STRIG(1) OR INKEY$<>"" THEN FORI=-1TO0:I=STRIG(0)ORSTRIG(1):NEXT:LOCATE18,16:PRINTSPC(14):RETURN 129
77 GOTO76
78 '******MAIN RUTINE ******
79 CLEAR1500,&HC000:DEFINTA-Z
80 AD=&HC000:FORI=0TO&H2F
81 POKE AD+I,VAL("&H"+MID$("2AF8F71141180620C5CD7401234F0608EBAFCB11CE9FCD77012310F57DC6186F300124C178FE112003214918EB10D9C9",I*2+1,2))
82 NEXT:DEFUSR=AD
83 SCREEN1,3,0:COLOR15,0,0:KEYOFF:WIDTH32
84 POKE&HFAFC,PEEK(&HFAFC)OR8
85 GS=&H38'PEEK(&HF3C6) '**sprite generator table
86 GA=&H78     '**new sprite generator table
87 ON STOP GOSUB 18:STOP ON
88 ON ERROR GOTO 64
89 POKE&HF3C6,GA:VDP(6)=GA\8
90 FORI=0TO7:BT(7-I)=2^I:NEXT
91 DEFFNA=PEEK(&HFBE5+6) AND (255+STRIG(3)*4) 'b2:GRAPH b1:CTRL b0:SHIFT
92 DEFFNB(X,Y)=GA*256+(PG*4+PT)*32+(XAND8)*2+Y
93 '****SET SCREEN ****
94 PRINT" - SPRITE EDITOR for SCREEN5 -"
95 VPOKE&H2000,&H5:VPOKE&H2002,&H30:VPOKE&H2003,&H20
96 PRINT" XWWWWWWYXWWWWWWY
97 FORI=0TO1
98  PRINT"X                Y"
99  FORJ=0TO5:PRINT"V                V":NEXT J
100  PRINT"Z                ["
101 NEXT I
102 PRINT" ZWWWWWW[ZWWWWWW[
103 FORI=0TO4
104   LOCATE 20,3+I
105  FORJ=0TO4:PRINTCHR$(1)+"@";:NEXT J
106 NEXT I
107 FORI=0TO5
108  READA$:LOCATE 20,9+I
109  PRINT HEX$(I);":";A$
110 NEXT I :DATA "SELECT","COPY","SAVE","LOAD","CLS","PAUSE"
111 'LOCATE 21,20:PRINT"Programmed"
112 'LOCATE 25,21:PRINT"by"
113 'LOCATE 22,22:PRINT"Uniskie"
114 'LOCATE 24,23:PRINT"Sarbunro";
115 FOR I=0TO1:LOCATE 18,15+I*3
116 FORJ=0TO13:PRINT"W";:NEXT:NEXT
117 FORI=0TO15:VPOKE 159*8+I,VAL("&H"+MID$("FE8282828282FE00FEFEFEFEFEFEFE00",I*2+1,2)):NEXT:VPOKE&H2013,&HE1:COLOR=(14,3,4,4)
118 FORI=0TO3:PUTSPRITE 1+I,(8+I*32,159),7,I:NEXT
119 PUTSPRITE 0,(164,27),1,0
120 PT=0:PG=0 'PATERN NUMBER = PG*4+PT
121 '**** EDIT SCREEN ****
122 GOSUB 7:LOCATE 1+PT*4,23:PRINT"EDIT";
123 LOCATE 19,8+NN:PRINT" "
124 '**** EDIT MAIN ****
125 LOCATE1+X,2+Y:PRINT"\"
126 A$=INKEY$:A=USR9(0):IF A$<"0" OR A$>"5" THEN 130
127 NN=ASC(A$)-47:LOCATE 19,8+NN:PRINT"*"
128 ON NN GOSUB 41,35,28,20,45,139
129 LOCATE 19,8+NN:PRINT" "
130 V=VPEEK(FNB(X,Y))
131 IF STRIG(0) OR STRIG(1) THEN V=VORBT(XAND7)
132 IF (FNA AND4)=0 THEN V=VAND(255-BT(XAND7))
133 VPOKE FNB(X,Y),V
134 LOCATE 1+X,2+Y:PRINT CHR$(160+((V AND BT(XAND7))=0))
135 S=STICK(0) OR STICK(1)
136 X=X+(S>5ANDX>0)-(S>1ANDS<5ANDX<15)
137 Y=Y+((S=8ORS>0ANDS<3)ANDY>0)-(S>3ANDS<7ANDY<15)
138 GOTO124
139 FORI=0TO3:VPOKE&H1B04+I*4,159:NEXT:COLOR=(15,1,2,2)
140 IF STRIG(0) THEN FOR I=-1 TO 0:I=STRIG(0):NEXT ELSE 140
141 VPOKE&H1B04+PT*4,151:COLOR=(15,7,7,7)
142 U=USR9(0):RETURN

