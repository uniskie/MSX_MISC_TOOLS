
DAC:		EQU	07F6H	;16 byte 演算の対象となる値を設定するエリア
INTDAC:		EQU	DAC+2	;2 byte USR引数のINT時の値を格納
NRDVRM:		EQU	0174H	;READ VRAM A=[HL]
NWRVRM:		EQU	0177H	;WRITE VRAM [HL]=A

PATNAM:		EQU	1800H	;SC1 PATNAM TABLE

BITCHR:		EQU	9FH	;"そ"

	ORG	0C000H

; スプライトのビットパターン16X16を読みだして
; 1ビットずつ1文字文字表示する
;
bit2char_16x16:
	LD	HL,(INTDAC)	;PATTERN ADDRESS
	LD	DE,PATNAM + 2*32 + 1	;LOCATE 1,2
	LD	B,20H
LOOP0:
	PUSH	BC
	CALL	NRDVRM
	INC	HL
	LD	C,A
	LD	B,8
	EX	DE,HL
LOOP1:
	XOR	A
	RL	C
	ADC	A,BITCHR
	CALL	NWRVRM
	INC	HL
	DJNZ	LOOP1

	LD	A,L
	ADD	A,18H
	LD	L,A
	JR	NC,.NC1
	INC	H
.NC1:
	POP	BC
	LD	A,B
	CP	11H
	JR	NZ,.NZ1
	LD	HL,PATNAM + 2*32 + 9	;LOCATE 9,2
.NZ1:
	EX	DE,HL
	DJNZ	LOOP0

	RET


