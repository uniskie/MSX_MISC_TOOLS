1000 '* SPRITE EDITOR forSCREEN5 ver2.3 *
1010 CLS:LOCATE,,0:PRINT"Sprite Editor for SCREEN5":DEFUSR9=&H156
1020 CLEAR1500,&HC000:DEFINTA-Z
1021 DEF FNC(A$)=A$=CHR$(13) OR A$=CHR$(27)
1022 SL$=CHR$(1)+CHR$(64):SL$=SL$+SL$:SL$=SL$+SL$
1030 GOTO 1750 '***** GOTO MAIN *****
1040 '** SPRITE PT **
1050 LOCATE 20,18:PRINT"PAGE:";HEX$(PG);"-";HEX$(PT)
1060 FORI=0TO3:VPOKE&H1B07+I*4,7:VPOKE&H1B06+I*4,(PG*4+I)*4:NEXT
1070 FORI=0TO4:LOCATE 0,19+I:PRINT SPC(20);:NEXT
1080 VPOKE&H1B07+PT*4,15:FORI=0TO3:LOCATEPT*4+1,20+I:PRINTSL$;:NEXT
1090 IF CO THEN RETURN
1100 ' PRINT PT
1110 VPOKE &H1B02,(PG*4+PT)*4
1120 LOCATE20,2:PRINT"No."+RIGHT$("0"+HEX$(PG*4+PT),2)
1130 A=GA*256+(PG*4+PT)*32
1140 A=USR(A):RETURN
1150 '** END **
1160 ONERRORGOTO0:POKE&HF3C6,GS:SCREEN,0,1:CLS:END
1170 '** LOAD DISK **
1180 LOCATE19,16:PRINT"LOAD(0-7)?";
1190 A$=INPUT$(1)
1200 IF FNC(A$) THEN 1240
1210 IF A$<"0" OR A$>"7" THEN GOSUB1590:GOTO 1190
1220 PRINTA$:BLOAD"SPRITE"+A$+".SC5",S
1230 LOCATE19,16:PRINT SPC(11):RETURN 2170
1240 GOSUB1590:LOCATE19,16:PRINT SPC(11):RETURN
1250 '** SAVE DISK **
1260 LOCATE19,16:PRINT"SAVE(0-7)?";
1270 A$=INPUT$(1)
1280 IF FNC(A$) THEN 1240
1290 IF A$<"0" OR A$>"7" THEN GOSUB1590:GOTO 1270
1300 PRINTA$:BSAVE"SPRITE"+A$+".SC5",GA*256,GA*256+32*64-1,S
1310 LOCATE19,16:PRINT SPC(11):RETURN
1320 '** COPY **
1330 LOCATE19,16:PRINT"COPY   No.":CO=1:GOSUB 1490:CO=0
1340 IF E THEN 1370
1350 FORI=0TO31:VPOKE GA*256+(PG*4+PT)*32+I,VPEEK(GA*256+(SG*4+ST)*32+I):NEXT
1360 PG=SG:PT=ST:GOSUB1040
1370 LOCATE18,16:PRINTSPC(14):RETURN 2180
1380 '** SELECT **
1390 LOCATE19,16:PRINT"SELECT No.":GOSUB 1490
1400 GOSUB1040:LOCATE18,16:PRINTSPC(14)
1410 IF E THEN RETURN 2180 ELSE RETURN 2180
1420 '** CLS **
1430 LOCATE21,16:PRINT"CLS?";
1440 A$=INPUT$(1):IF FNC(A$) THEN GOSUB 1590:GOTO 1480
1450 IF A$<>" " THEN GOSUB 1590:GOTO 1440
1460 FORI=0TO31:VPOKEGA*256+(PG*4+PT)*32+I,0:NEXT
1470 LOCATE21,16:PRINTSPC(4):RETURN 2170
1480 LOCATE21,16:PRINTSPC(4):RETURN
1490 '**SELECT SUB **
1500 SG=PG:ST=PT:E=0:LOCATE 1+PT*4,19:PRINT"(??)";
1510 A$=INKEY$:IF FNC(A$) OR(FNAAND4)=0 THEN GOSUB1590:PG=SG:PT=ST:E=1:RETURN
1520 LOCATE 29,16:PRINT RIGHT$("0"+HEX$(PG*4+PT),2)
1530 IF STRIG(0)OR STRIG(1) THEN RETURN
1540 S=STICK(0)OR STICK(1):IF S=0 THEN 1510
1550 PG=(PG+(S=5)-(S=1))AND15
1560 PT=(PT+(S=7)-(S=3))AND3
1570 GOSUB 1040:LOCATE 1+PT*4,19:PRINT"(??)";
1580 GOTO1510
1590 '** ERROR FLASH **
1600 COLOR=(0,7,7,7):COLOR=(0,0,0,0):RETURN
1610 '** DISK ERROR **
1620 ER=ERR:EL=ERL:A$=""
1630 GOSUB1590
1640 IF ER=53 THEN A$="File not found"
1650 IF ER=68 THEN A$="Write protect "
1660 IF ER=70 THEN A$=" Disk offline "
1670 IF ER=69 THEN A$="Disk I/O error"
1680 IF ER=67 THEN A$="File too Full "
1690 IF ER=60 THEN A$="   Bad fat    "
1700 IF A$="" THEN SCREEN,0,1:CLS:ON ERROR GOTO 0:END
1710 RESUME 1720
1720 LOCATE18,16:PRINT A$
1730 IF STRIG(0) OR STRIG(1) OR INKEY$<>"" THEN FORI=-1TO0:I=STRIG(0)ORSTRIG(1):NEXT:LOCATE18,16:PRINTSPC(14):RETURN 2250
1740 GOTO1730
1750 '******MAIN RUTINE ******
1760 AD=&HC000:FORI=0TO&H2F
1770 POKE AD+I,VAL("&H"+MID$("2AF8F71141180620C5CD7401234F0608EBAFCB11CE9FCD77012310F57DC6186F300124C178FE112003214918EB10D9C9",I*2+1,2))
1780 NEXT:DEFUSR=AD
1790 SCREEN1,3,0:COLOR15,0,0:KEYOFF:WIDTH32
1800 POKE&HFAFC,PEEK(&HFAFC)OR8
1810 GS=&H38'PEEK(&HF3C6) '**sprite generator table
1820 GA=&H78'** SprPatGen - same as SC5
1830 ON STOP GOSUB 1150:STOP ON
1840 ON ERROR GOTO 1610
1850 POKE&HF3C6,GA:VDP(6)=GA\8
1860 FORI=0TO7:BT(7-I)=2^I:NEXT
1870 DEF FNA=PEEK(&HFBE5+6) AND (255+STRIG(3)*4) 'b2:GRAPH b1:CTRL b0:SHIFT
1880 DEF FNB(X,Y)=GA*256+(PG*4+PT)*32+(XAND8)*2+Y
1890 '****SET SCREEN ****
1900 PRINT" - SPRITE EDITOR for SCREEN5 -"
1910 VPOKE&H2000,&HE:VPOKE&H2002,&H30:VPOKE&H2003,&H20:VPOKE&H2005,&HE0:VPOKE&H2006,&H70:VPOKE&H2007,&H50
1920 PRINT" XWWWWWWYXWWWWWWY
1930 FORI=0TO1
1940  PRINT"X                Y"
1950  FORJ=0TO5:PRINT"V                V":NEXT J
1960  PRINT"Z                ["
1970 NEXT I
1980 PRINT" ZWWWWWW[ZWWWWWW[
1990 FORI=0TO4
2000   LOCATE 20,3+I
2010  FORJ=0TO4:PRINT"@";:NEXT J
2020 NEXT I
2030 FORI=0TO5
2040  READA$:LOCATE 20,9+I
2050  PRINT HEX$(I);":";A$
2060 NEXT I :DATA "SELECT","COPY","SAVE","LOAD","CLS","PAUSE"
2070 'LOCATE 21,20:PRINT"Programmed"
2080 'LOCATE 25,21:PRINT"by"
2090 'LOCATE 22,22:PRINT"Uniskie"
2100 'LOCATE 24,23:PRINT"Sarbunro";
2110 FOR I=0TO1:LOCATE 18,15+I*3
2120 FORJ=0TO13:PRINT"W";:NEXT:NEXT
2130 FORI=0TO15:VPOKE 159*8+I,VAL("&H"+MID$("FE8282828282FE00FEFEFEFEFEFEFE00",I*2+1,2)):NEXT:VPOKE&H2013,&HE1:COLOR=(14,3,4,4)
2140 FORI=0TO3:PUTSPRITE 1+I,(8+I*32,159),7,I:NEXT
2150 PUTSPRITE 0,(164,27),1,0
2160 PT=0:PG=0 'PATERN NUMBER = PG*4+PT
2170 '**** EDIT SCREEN ****
2180 GOSUB 1040:LOCATE 1+PT*4,19:PRINT"EDIT";
2190 LOCATE 19,8+NN:PRINT" "
2200 '**** EDIT MAIN ****
2210 LOCATE1+X,2+Y:PRINT"\"
2220 A$=INKEY$:A=USR9(0):IF A$<"0" OR A$>"5" THEN 2260
2230 NN=ASC(A$)-47:LOCATE 19,8+NN:PRINT"*"
2240 ON NN GOSUB 1380,1320,1250,1170,1420,2350
2250 LOCATE 19,8+NN:PRINT" "
2260 V=VPEEK(FNB(X,Y))
2270 IF STRIG(0) OR STRIG(1) THEN V=VORBT(XAND7)
2280 IF (FNA AND4)=0 THEN V=VAND(255-BT(XAND7))
2290 VPOKE FNB(X,Y),V
2300 LOCATE 1+X,2+Y:PRINT CHR$(160+((V AND BT(XAND7))=0))
2310 S=STICK(0) OR STICK(1)
2320 X=X+(S>5ANDX>0)-(S>1ANDS<5ANDX<15)
2330 Y=Y+((S=8ORS>0ANDS<3)ANDY>0)-(S>3ANDS<7ANDY<15)
2340 GOTO2200
2350 FORI=0TO3:VPOKE&H1B04+I*4,159:NEXT:COLOR=(15,1,2,2)
2360 IF STRIG(0) THEN FOR I=-1 TO 0:I=STRIG(0):NEXT ELSE 2360
2370 VPOKE&H1B04+PT*4,151:COLOR=(15,7,7,7)
2380 U=USR9(0):RETURN

