10 ' VDPÚ¼Þ½À(WORKAREA)É ±À²¶× 
20 ' ¶ÞÒÝÓ°ÄÞ¦ ÊÝÃ² ¼¤
30 ' VRAM É [ÊßÚ¯ÄÃ°ÌÞÙ] ±ÄÞÚ½ ¦ ¶´½.
40 '
50 ' -- TEST --
60 CLEAR 500:DEFINT A-Z
70 MS$="SCREEN 0:WIDTH 40":SCREEN 0:WIDTH 40:GOSUB 150
80 MS$="SCREEN 0:WIDTH 80":SCREEN 0:WIDTH 80:GOSUB 150
90 FOR S=1 TO 12
100  IF S=9 THEN 120
110  MS$="SCREEN"+STR$(S):SCREEN S:GOSUB 150
120 NEXT
130 END
140 '
150 GOSUB 1000:M1$=M$:A1=AD ' CHECK TYPE [COMPACT]
160 GOSUB 2000:M2$=M$:A2=AD ' CHECK TYPE [BASIC]
170 SCREEN 0:PRINT "-- ";MS$;" --":PRINT
180 PRINT M1$;":&H";HEX$(A1):PRINT "  ( M=";M;")":PRINT
190 PRINT M2$;":&H";HEX$(A2):PRINT "  ( V0=";V0;" V1=";V1;" V2=";V2;")":PRINT
200 PRINT "-- PUSH ANY KEY --";:I$=INPUT$(1)
210 '
1000 '--------------------------------------
1010 '--GET VRAM PALETTE ADDRESS (compact)--
1020 '--------------------------------------
1030 RESTORE 1060:M=(VDP(1)AND 16)/16+(VDP(1)AND 8)/4+(VDP(0)AND 14)*2+(VDP(26)AND 12)*4
1040 READ AD,I,M$:IF AD=0 THEN RETURN
1050 IF I=M THEN RETURN ELSE 1040
1060 DATA &H0400,&B0000001,TEXT1(SCREEN0 WIDTH40)
1070 DATA &H0F00,&B0001001,TEXT2(SCREEN0 WIDTH80)
1080 DATA &H2020,&B0000010,MULT COLOR(SCREEN3)
1090 DATA &H2020,&B0000000,GRAPHIC1(SCREEN1)
1100 DATA &H1B80,&B0000100,GRAPHIC2(SCREEN2)
1110 DATA &H1B80,&B0001000,GRAPHIC3(SCREEN4)
1120 DATA &H7680,&B0001100,GRAPHIC4(SCREEN5)
1130 DATA &H7680,&B0010000,GRAPHIC5(SCREEN6/9)
1140 DATA &HFA80,&B0010100,GRAPHIC6(SCREEN7)
1150 DATA &HFA80,&B0011100,GRAPHIC7(SCREEN8)
1160 DATA &HFA80,&B1111100,YJK/RGB(SCREEN10/11)
1170 DATA &HFA80,&B0111100,YJK(SCREEN12)
1180 DATA 0,0
1190 '
2000 '-----------------------------------------
2010 '--GET VRAM PALETTE ADDRESS (BASIC TYPE)--
2020 '-----------------------------------------
2030 V0=(VDP(0)AND &B00001110)/2 ' M5 M4 M3
2040 V1=(VDP(1)AND &B00011000)/8 ' M1 M2
2050 V2=(VDP(26)AND &B00011000)/8' YAE YJK
2060 RESTORE 2090
2070 READ AD,I0,I1,I2,M$:IF AD=0 THEN RETURN
2080 IF(I0=V0)AND(I1=V1)AND(I2=V2)THEN RETURN ELSE 2070
2090 '--- PAL_ADR, V0, V1, V2, NAME
2100 DATA &H0400,&B000,&B10,&B00,TEXT1(SCREEN0 WIDTH40)
2110 DATA &H0F00,&B010,&B10,&B00,TEXT2(SCREEN0 WIDTH80)
2120 DATA &H2020,&B000,&B01,&B00,MULT COLOR(SCREEN3)
2130 DATA &H2020,&B000,&B00,&B00,GRAPHIC1(SCREEN1)
2140 DATA &H1B80,&B001,&B00,&B00,GRAPHIC2(SCREEN2)
2150 DATA &H1B80,&B010,&B00,&B00,GRAPHIC3(SCREEN4)
2160 DATA &H7680,&B011,&B00,&B00,GRAPHIC4(SCREEN5)
2170 DATA &H7680,&B100,&B00,&B00,GRAPHIC5(SCREEN6/9)
2180 DATA &HFA80,&B101,&B00,&B00,GRAPHIC6(SCREEN7)
2190 DATA &HFA80,&B111,&B00,&B00,GRAPHIC7(SCREEN8)
2200 DATA &HFA80,&B111,&B00,&B11,YJK/RGB(SCREEN10/11)
2210 DATA &HFA80,&B111,&B00,&B01,YJK(SCREEN12)
