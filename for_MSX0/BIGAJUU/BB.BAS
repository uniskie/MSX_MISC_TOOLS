1000 CLEAR 200,&HC000:DEFINT A-Z
1010 '********************************
1020 '* AE : MSX0 IOT & MPU06886
1030 '********************************
1040 AE=0:ON STOP GOSUB 3190:STOP ON:ON ERROR GOTO 1100
1050 '_IOTPUT("msx/me/pm/cpu/percent",800)
1060 ND$="device/i2c_i":_IOTFIND(ND$,CN):IF CN<1 THEN 1110
1070 DIM D$(CN-1):_IOTFIND(ND$,D$(0),CN)
1080 FOR I=0 TO CN-1:AE=AE OR (D$(I)="68"):NEXT '68=MPU6886
1090 ERASE D$:GOTO 1110
1100 RESUME 1110
1110 ON ERROR GOTO 0 'END of CHECK
1120 '********************************
1130 SC=5:A=RND(-TIME)
1140 COLOR 15,13,0:VDP(9)=VDP(9)AND253':VDP(9)=VDP(9)OR2
1150 SCREEN SC':VDP(10)=VDP(10)AND 127'192 line mode
1160 FOR I=0TO14:COLOR=(I,0,0,0):NEXT
1170 DEFUSR=&H156
1180 BLOAD"SRX2.BIN"
1190 UA=&HD000:DIM UA(14):FOR I=0 TO 14:UA(I)=UA+I*3:NEXT
1200 DEFUSR1=UA(0) 'LOAD SRD
1210 DEFUSR2=UA(1) 'SET PAL
1220 DEFUSR3=UA(2) 'TIMER SPRITE
1230 DEFUSR4=UA(3) 'VDP CMD
1240 DEFUSR5=UA(4) 'WAIT VDP CMD
1250 DEFUSR6=UA(5) 'PUT SPRITE
1260 DEFUSR7=UA(6) 'SET SPRITE PATTERN COLOR TABLE
1270 DEFUSR8=UA(7) 'SET VSYNC HOOK for SPRITE (-1=RESTORE)
1280 POKE UA(14)+2,0:POKE UA(14)+3,&HC0
1290 POKE UA(14)+4,0:POKE UA(14)+5,&H10
1300 F$="BIGAJUU":F$=LEFT$(F$+SPACE$(8),8)
1310 DIM PL(127):COPY F$+".PL"+HEX$(SC) TO PL
1320 SETPAGE,2:CLS:SETPAGE,3:CLS
1330 SETPAGE0,1:A$=USR1(F$+".SR"+HEX$(SC)):SETPAGE0,0
1340 SCREEN,3:BLOAD"SPRITE0.SC5",S,-(SC>6)*&H7800
1350 '
1360 '********************************
1370 TR=1:STOP OFF:ON ERROR GOTO 3140
1380 _TURBOON( AE, TR, UA, UA(), SC, PL() )
1390 '********************************
1400 DIM EX(1),EY(1) ' EYE
1410 DIM VD(7,7)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
1420 DIM VC(7,2)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
1430 DIM SC(16*8-1)'SPRITE COLOR by PATTERN NUMBER
1440 DIM SR(32*4-1)'SPRITE ATTRIBUTE TABLE
1450 SA=&H7600'SPRITE ATTRIBUTE TABLE
1460 KM=&HFBE5'NEWKEY(11)
1470 TB=1
1480 '
1490 ' CALC PALETTE FADE OUT
1500 PL(0)=33:FOR I=1 TO 7:FOR J=0 TO 15:A=I*16+J:PL=PL(A-16):G=PL\256:R=(PL\16)AND 15:B=PL AND 15:B=(B-1)*-(B>0):R=(R-1)*-(R>0):G=(G-1)*-(G>0):PL(A)=B+R*16+G*256:NEXT J:NEXT I
1510 '
1520 'ﾎﾝﾀｲ ｺﾋﾟｰ ｻｷ ﾉ ｻﾞﾋｮｳ
1530 OX=0:OY=0:SX=0:SY=0:SW=256:SH=192:SP=256:BP=768*0:DX=OX-SX:DY=OY-SY
1540 'ｼﾀ ﾉ ｱﾆﾒｰｼｮﾝ
1550 AM=2:XM=1-2*(SC<7)
1560 RESTORE 1700:READ NX,NY,DX,DY
1570 FOR I=0 TO AM
1580   READ CX,CY
1590   VC(0,I)=CX:VC(1,I)=CY+SP 'SX,SY
1600   VC(2,I)=DX+OX:VC(3,I)=DY+OY+BP 'DX,DY
1610   VC(4,I)=NX:VC(5,I)=NY 'NX,NY
1620   VC(6,I)=0 'COLOR+ARG*256
1630   'L=(VC(0,I)OR VC(2,I)OR VC(4,I))AND XM
1640   'IF L=0 THEN VC(7,I)=&HD0 'CMD HMMM
1650   'IF L THEN VC(7,I)=&H90 'CMD LMMM
1660   'VC(7,I)=&H98 'CMD lMMM TPSET
1670   VC(7,I)=&HD0 'CMD HMMM
1680  NEXT I
1690 '     NX,NY    DX,DY     SX,SY*n
1700 DATA  64,40,   96,136,   0,192, 64,192, 128,192
1710 '
1720 'ﾎﾝﾀｲ ｺﾋﾟｰｼﾞｭﾝﾋﾞ
1730 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
1740 A=7:VD(0,A)=SX:VD(1,A)=SY+SP:VD(2,A)=OX:VD(3,A)=OY+BP:VD(4,A)=SW:VD(5,A)=SH:VD(6,A)=0:VD(7,A)=&HD0 'HMMM
1750 '
1760 'CLOCK
1770 C$=STRING$(1,15)+STRING$(9,2)+STRING$(6,3)
1780 FOR I=1 TO 14
1790  A=VARPTR(SC(I*8))
1800  FOR J=0 TO 15:POKE A+J,ASC(MID$(C$,J+1,1)):NEXT J
1810 NEXT I
1820 FOR J=0 TO 7:SC(15*8+J)=&H101:NEXT J:SC(15*8+6)=&H401:SC(15*8+7)=&H507
1830 U=USR7(VARPTR(SC(0)))
1840 FORI=0TO31:SR(I*4)=216:NEXT I
1850 X=(256-8*20)\2
1860 FOR I=0 TO 7:J=I*4
1870  'PUTSPRITE I,(X+I*20,180),2,0
1880  'COLORSPRITE$(I)=STRING$(4,7)+STRING$(4,3)
1890  SR(J+0)=180*2
1900  SR(J+1)=(X+I*20)*2
1910  SR(J+2)=0
1920  SR(J+3)=0
1930 NEXT I
1940 '
1950 'EYE
1960 EX(0)=66:EY(0)=38:EX(1)=156:EY(1)=42
1970 FOR I=0 TO 1:J=I*4+8*4
1980  'PUTSPRITE 8+I,(EX(I),EY(I)),1,15
1990  SR(J+0)=EY(I)*2
2000  SR(J+1)=EX(I)*2
2010  SR(J+2)=15
2020  SR(J+3)=0
2030 NEXT I
2040 '
2050 'U=USR8(2) 'SPRITE SWAP MODE
2060 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
2070 '
2080 PN=7:GOSUB 2710'PALETTE
2090 TIME=0:FORI=0TO1:I=TIME:NEXT I
2100 LINE(0,192)-(255,211),0,BF
2110 '
2120 'ﾎﾝﾀｲ ｺﾋﾟｰ
2130 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
2140 A=7:VD(7,A)=&HD0:GOSUB 2600'HMMM
2150 '
2160 ONINTERVAL=5 GOSUB 2630:GOSUB 2630
2170 '
2180 'VDP(26)=2
2190 FORI=0TO7:PN=(7-I):GOSUB 2710:TIME=0:FORJ=0TO1:J=TIME:NEXT J:NEXT I
2200 F1=-1' 1ST FLAG
2210 '
2220 'MAIN LOOP
2230 'U=USR5(0)'WAIT VDP COMMAND
2240 S=(1-(PEEK(KM+6)AND 2))*(16-(PEEK(KM+7)AND 16)) 'CTRL+STOP
2250 S=S+4-(PEEK(KM+7)AND4) 'ESC
2260 IF S THEN 3060 ' END
2270 '
2280 IF TIME<1 THEN 2280 ELSE TIME=0
2290 '
2300 ' (ANIMATION)
2310 AN=(AN+1)MOD(AM+1):A=AN:GOSUB 2570
2320 '
2330 ON 1+AE GOTO 2410
2340 ND$="device/accel/x":GOSUB 2920:X=A'_IOTGET(ND$,X)
2350 ND$="device/accel/y":GOSUB 2920:Y=A'_IOTGET(ND$,Y)
2360 ND$="device/accel/z":GOSUB 2920:Z=A'_IOTGET(ND$,Z)
2370 VX=X\32:VY=Y\32:VZ=Z\32
2380 ON 1-YM GOTO 2390:SWAP Y,Z:SWAP VY,VZ
2390 ON 1+(STRIG(0) OR STRIG(1) OR PAD(4) OR F1) GOTO 2410
2400 F1=0:BX=VX:BY=VY:IF ABS(Y)>800 THEN YM=1-YM:BY=VZ:SWAP VY,VZ
2410 ON 1+PAD(4) GOTO 2450 'TOUCH PAD2
2420 EX=(EX+(PAD(5)-128)/4)\2
2430 EY=(EY+(PAD(6)-106)/4)\2
2440 GOTO 2460
2450 EX=-VX+BX:EY=VY-BY 'ACCEL
2460 SR(8*4)  =EY(0)*2+EY
2470 SR(8*4+1)=EX(0)*2+EX
2480 SR(9*4  )=EY(1)*2+EY
2490 SR(9*4+1)=EX(1)*2+EX
2500 '
2510 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
2520 GOTO 2230
2530 '
2540 '------ COMMON
2550 '
2560 ' COPY VC
2570 U=USR4(VARPTR(VC(0,A))):RETURN
2580 '
2590 ' COPY VD
2600 U=USR4(VARPTR(VD(0,A))):RETURN
2610 '
2620 'TIMER
2630 INTERVAL OFF
2640 TC=(TC+1)AND 2047
2650 IF TC AND 3 THEN 2670
2660 U=USR3(VARPTR(SR(0))) 'TIME SPRITE
2670 INTERVAL ON
2680 RETURN
2690 '
2700 'PALETTE (PN=PALETTE NO)
2710 PJ=PN*16
2720 U=USR2(VARPTR(PL(PJ)))
2730 RETURN
2740 '
2750 '------ IOT
2760 '
2770 'NODE SELECT:ND$
2780 OUT 8,&HE0:OUT 8,1:OUT 8,&H53:IO$=ND$:GOSUB 2830
2790 NC=INP(8):ER=0:IF NC AND 128 THEN ER=19'ERROR 19'DEVICE I/O ERROR
2800 RETURN
2810 '
2820 'OUTPUT STRING(IO$)
2830 OUT 8,&HC0:NL=LEN(IO$):NH=1
2840 IF (NL-NH)<63 THEN 2860
2850 OUT 8,64:FOR NI=NH TO NH+64:OUT 8,ASC(MID$(IO$,NI,1)):NEXT NI:NH=NH+64:GOTO 2840
2860 OUT 8,NL-NH+1:FOR NI=NH TO NL:OUT 8,ASC(MID$(IO$,NI,1)):NEXT NI:OUT 8,0:RETURN
2870 '
2880 'OUTPUT INT(A)
2890 OUT 8,&HC0:OUT 8,2:OUT 8,A AND 255:OUT 8,PEEK(VARPTR(A)+1):OUT 8,0:RETURN
2900 '
2910 'IOTGET ND$,A
2920 GOSUB 2780 'SELECT NODE
2930 OUT 8,&HE0:OUT 8,1:OUT 8,1'IOTGET($00)+INT(1)
2940 OUT 8,&H80:NL=INP(8):A=INP(8):POKE(VARPTR(A)+1),INP(8)'READ 2 BYTE
2950 RETURN
2960 '
2970 'IOTGET ND$,A$
2980 GOSUB 2780 'SELECT NODE
2990 OUT 8,&HE0:OUT 8,1:OUT 8,3'IOTGET($00)+STR(3)
3000 OUT 8,&H80:NL=INP(8)'READ DATA SIZE
3010 A$="":FOR NI=1 TO NL:A$=A$+CHR$(INP(8)):NEXT NI
3020 RETURN
3030 '
3040 '********************************
3050 '対応必須な処理は_TURBOOFF前に実行する
3060 INTERVAL OFF:U=USR8(-1):TR=0'TR=0にしておくとエラー発生で終了処理に飛ぶ
3070 '_TURBOOFFまでにCTRL+STOPが入力されていると_TURBOOFF直後に中断される
3080 _TURBOOFF
3090 'べーしっ君が無い場合はここに飛ばずにTR=0で3050へ飛ぶ
3100 ON ERROR GOTO 0:STOP OFF:END
3110 '********************************
3120 '
3130 '*** No XBASIC (-> NORMAL MODE)
3140 RESUME 3160
3150 ' TR=1でエラー発生した場合はノーマルモードとして続行する
3160 IF TR THEN TR=0:ON ERROR GOTO 0:STOP ON:GOTO 1400 'NORMAL MODEで続行
3170 ' TR=0でエラー発生した場合はこのまま終了処理へ
3180 '*** STOP hook (only NORMAL MODE)
3190 ON ERROR GOTO 0:STOP OFF:INTERVAL OFF:U=USR8(-1):END
