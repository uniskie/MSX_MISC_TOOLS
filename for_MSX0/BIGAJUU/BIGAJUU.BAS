10 CLEAR 200,&HB000:DEFINT A-Z
20 SC=5:A=RND(-TIME)
30 COLOR 15,13,0:VDP(9)=VDP(9)AND253':VDP(9)=VDP(9)OR2
40 SCREEN SC':VDP(10)=VDP(10)AND 127'192 line mode
50 FOR I=0TO14:COLOR=(I,0,0,0):NEXT
60 DEFUSR=&H156
70 BLOAD"SRX2.BIN"
80 DEFUSR1=&HC000 'LOAD SRD
90 DEFUSR2=&HC003 'SET PAL
100 DEFUSR3=&HC006 'TIMER SPRITE
110 DEFUSR4=&HC009 'VDP CMD
120 DEFUSR5=&HC00C 'WAIT VDP CMD
130 DEFUSR6=&HC00F 'PUT SPRITE
140 DEFUSR7=&HC012 'SET SPRITE PATTERN COLOR TABLE
150 F$="BIGAJUU":F$=LEFT$(F$+SPACE$(8),8)
160 DIM PL(255):COPY F$+".PL"+HEX$(SC) TO PL
170 PL(0)=33:FOR I=1 TO 7:FOR J=0 TO 15:A=I*32+J*2:B=PL(A-32)\16:R=PL(A-32)AND15:G=PL(A-31):B=(B-1)*-(B>0):R=(R-1)*-(R>0):G=(G-1)*-(G>0):PL(A)=B*16+R:PL(A+1)=G:NEXT:NEXT
180 SETPAGE,2:CLS:SETPAGE,3:CLS
190 SETPAGE0,1:A$=USR1(F$+".SR"+HEX$(SC)):SETPAGE0,0
200 SCREEN,3:BLOAD"SPRITE0.SC5",S,-(SC>6)*&H7800
210 '
220 'ÎÝÀ² ºËß° »· É »ÞË®³
230 OX=0:OY=0:SX=0:SY=0:SW=256:SH=192:SP=256:BP=768*0:DX=OX-SX:DY=OY-SY
240 '¼À É ±ÆÒ°¼®Ý
250 AM=2:XM=1-2*(SC<7)
260 DIM VC(7,AM)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
270 DIM D(3):RESTORE 420
280 READ D(0),D(1),D(2),D(3)
290 FOR I=0 TO AM
300   READ CX,CY
310   VC(0,I)=CX:VC(1,I)=CY+SP 'SX,SY
320   VC(2,I)=D(2)+OX:VC(3,I)=D(3)+OY+BP 'DX,DY
330   VC(4,I)=D(0):VC(5,I)=D(1) 'NX,NY
340   VC(6,I)=0 'COLOR+ARG*256
350   L=(VC(0,I)OR VC(2,I)OR VC(4,I))AND XM
360   IF L=0 THEN VC(7,I)=&HD0 'CMD HMMM
370   IF L THEN VC(7,I)=&H90 'CMD LMMM
380   'VC(7,J,I)=&H98 'CMD lMMM TPSET
390  NEXT
400 ERASE D
410 '     NX,NY    DX,DY     SX,SY*n
420 DATA  64,40,   96,136,   0,192, 64,192, 128,192
430 '
440 DIM VD(7,7)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
450 'ÎÝÀ² ºËß°¼Þ­ÝËÞ
460 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
470 A=7:VD(0,A)=SX:VD(1,A)=SY+SP:VD(2,A)=OX:VD(3,A)=OY+BP:VD(4,A)=SW:VD(5,A)=SH:VD(6,A)=0:VD(7,A)=&HD0 'HMMM
480 '
490 'CLOCK
500 DIM SC(16*8-1)'SPRITE COLOR by PATTERB NO
510 C$=STRING$(4,7)+STRING$(4,3)+STRING$(8,2)
520 FOR I=1 TO 14
530  A=VARPTR(SC(I*8))
540  FOR J=0 TO 15:POKE A+J,ASC(MID$(C$,J+1,1)):NEXT
550 NEXT
560 FOR J=0 TO 7:SC(15*8+J)=&H101:NEXT
570 U=USR7(VARPTR(SC(0)))
580 DIM SR(32*4-1):SA=&H7600'SPRITE ATTRIBUTE TABLE
590 X=(256-8*20)\2
600 FOR I=0 TO 7:J=I*4
610  PUTSPRITE I,(X+I*20,180),2,0
620  COLORSPRITE$(I)=STRING$(4,7)+STRING$(4,3)
630  SR(J+0)=VPEEK(SA+J+0)*2
640  SR(J+1)=VPEEK(SA+J+1)*2
650  SR(J+2)=VPEEK(SA+J+2)\4
660  SR(J+3)=0
670 NEXT
680 '
690 DIM EX(1),EY(1) ' EYE
700 EX(0)=66:EY(0)=38:EX(1)=156:EY(1)=42
710 FOR I=0 TO 1:J=I*4+8*4
720  PUTSPRITE 8+I,(EX(I),EY(I)),1,15
730  SR(J+0)=VPEEK(SA+J+0)*2
740  SR(J+1)=VPEEK(SA+J+1)*2
750  SR(J+2)=VPEEK(SA+J+2)\4
760  SR(J+3)=0
770 NEXT
780 '
790 LINE(0,192)-(255,211),0,BF
800 '
810 ONINTERVAL=8 GOSUB 1210:GOSUB 1210
820 '
830 'A$="t80l8s0m20000o5 ge-o4b-o5e- fb-4. fgfo4b-o5e-2"
840 'B$="t80l8v13o4 r4g4o5d4.o4f b-4 b-4 sm20000g2"
850 'PLAY A$,B$
860 '
870 FORI=0TO7:J=(7-I)*32:U=USR2(VARPTR(PL(J))):NEXT
880 '
890 'ÎÝÀ² ºËß°
900 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
910 A=7:VD(7,A)=&HD0:GOSUB 1180'HMMM
920 TIME=0:TM=TIME:TB=TM
930 'VDP(26)=2
940 '
950 'MAIN LOOP
960 'U=USR5(0)'WAIT VDP COMMAND
970 I$=INKEY$:A=USR(0)
980 TIME=TIME AND 255:TM=TIME
990 'ON -(TB>TM) GOTO 810
1000 TB=TM
1010 '
1020 ' (ANIMATION)
1030 AN=(AN+1)MOD(AM+1):A=AN:GOSUB 1150
1040 '
1050 SR(8*4)=EY(0)*2+(TM AND 3)
1060 SR(9*4)=EY(1)*2+(TM AND 3)
1070 '
1080 'U=USR3(VARPTR(SR(0))) 'TIME SPRITE
1090 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
1100 GOTO 960
1110 '
1120 '------ COMMON
1130 '
1140 ' COPY VC
1150 U=USR4(VARPTR(VC(0,A))):RETURN
1160 '
1170 ' COPY VD
1180 U=USR4(VARPTR(VD(0,A))):RETURN
1190 '
1200 'TIMER
1210 INTERVAL OFF
1220 TC=(TC+1)AND 2047
1230 IF TC AND 3 THEN 1250
1240 U=USR3(VARPTR(SR(0))) 'TIME SPRITE
1250 INTERVAL ON
1260 RETURN
1270 '
1280 'PALETTE (PN=PALETTE NO)
1290 PJ=PN*16
1300 U=USR2(VARPTR(PL(PJ)))
1310 RETURN
1320 '
1330 '------ COMMON
