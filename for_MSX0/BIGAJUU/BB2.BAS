1000 CLEAR 200,&HC000:DEFINT A-Z
1010 '********************************
1020 '* AE : MSX0 IOT & MPU06886
1030 '********************************
1040 AE=0:ON ERROR GOTO 1100
1050 '_IOTPUT("msx/me/pm/cpu/percent",800)
1060 ND$="device/i2c_i":_IOTFIND(ND$,CN):IF CN<1 THEN 1110
1070 DIM D$(CN-1):_IOTFIND(ND$,D$(0),CN)
1080 FOR I=0 TO CN-1:AE=AE OR (D$(I)="68"):NEXT '68=MPU6886
1090 ERASE D$:GOTO 1110
1100 RESUME 1110
1110 ON ERROR GOTO 0 'END of CHECK
1120 '********************************
1130 SC=5:A=RND(-TIME)
1140 COLOR 15,13,0:VDP(9)=VDP(9)AND253':VDP(9)=VDP(9)OR2
1150 SCREEN SC':VDP(10)=VDP(10)AND 127'192 line mode
1160 FOR I=0TO14:COLOR=(I,0,0,0):NEXT
1170 DEFUSR=&H156
1180 BLOAD"SRX2.BIN"
1190 UA=&HD000:DIM UA(14):FOR I=0 TO 14:UA(I)=UA+I*3:NEXT
1200 DEFUSR1=UA(0) 'LOAD SRD
1210 DEFUSR2=UA(1) 'SET PAL
1220 DEFUSR3=UA(2) 'TIMER SPRITE
1230 DEFUSR4=UA(3) 'VDP CMD
1240 DEFUSR5=UA(4) 'WAIT VDP CMD
1250 DEFUSR6=UA(5) 'PUT SPRITE
1260 DEFUSR7=UA(6) 'SET SPRITE PATTERN COLOR TABLE
1270 DEFUSR8=UA(7) 'SET VSYNC HOOK for SPRITE (-1=RESTORE)
1280 POKE UA(14)+2,0:POKE UA(14)+3,&HC0
1290 POKE UA(14)+4,0:POKE UA(14)+5,&H10
1300 ON STOP GOSUB 3310:STOP ON
1310 '
1320 F$="BIGAJUU":F$=LEFT$(F$+SPACE$(8),8)
1330 DIM PL(127):COPY F$+".PL"+HEX$(SC) TO PL
1340 SETPAGE,2:CLS:SETPAGE,3:CLS
1350 SETPAGE0,1:A$=USR1(F$+".SR"+HEX$(SC)):SETPAGE0,0
1360 SCREEN,3:BLOAD"SPRITE0.SC5",S,-(SC>6)*&H7800
1370 '
1380 '********************************
1390 TR=1:STOP ON':ON ERROR GOTO 2270
1400 _TURBOON( AE, TR, UA, UA(), SC, PL() )
1410 '********************************
1420 DIM EX(1),EY(1) ' EYE
1430 DIM VD(7,7)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
1440 DIM VC(7,2)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
1450 DIM SC(16*8-1)'SPRITE COLOR by PATTERN NUMBER
1460 DIM SR(32*4-1)'SPRITE ATTRIBUTE TABLE
1470 SA=&H7600'SPRITE ATTRIBUTE TABLE
1480 KM=&HFBE5'NEWKEY(11)
1490 TB=1
1500 '
1510 ' CALC PALETTE FADE OUT
1520 PL(0)=33:FOR I=1 TO 7:FOR J=0 TO 15:A=I*16+J:PL=PL(A-16):G=PL\256:R=(PL\16)AND 15:B=PL AND 15:B=(B-1)*-(B>0):R=(R-1)*-(R>0):G=(G-1)*-(G>0):PL(A)=B+R*16+G*256:NEXT J:NEXT I
1530 '
1540 'ﾎﾝﾀｲ ｺﾋﾟｰ ｻｷ ﾉ ｻﾞﾋｮｳ
1550 OX=0:OY=0:SX=0:SY=0:SW=256:SH=192:SP=256:BP=768*0:DX=OX-SX:DY=OY-SY
1560 'ｼﾀ ﾉ ｱﾆﾒｰｼｮﾝ
1570 AM=2:XM=1-2*(SC<7)
1580 RESTORE 1720:READ NX,NY,DX,DY
1590 FOR I=0 TO AM
1600   READ CX,CY
1610   VC(0,I)=CX:VC(1,I)=CY+SP 'SX,SY
1620   VC(2,I)=DX+OX:VC(3,I)=DY+OY+BP 'DX,DY
1630   VC(4,I)=NX:VC(5,I)=NY 'NX,NY
1640   VC(6,I)=0 'COLOR+ARG*256
1650   'L=(VC(0,I)OR VC(2,I)OR VC(4,I))AND XM
1660   'IF L=0 THEN VC(7,I)=&HD0 'CMD HMMM
1670   'IF L THEN VC(7,I)=&H90 'CMD LMMM
1680   'VC(7,I)=&H98 'CMD lMMM TPSET
1690   VC(7,I)=&HD0 'CMD HMMM
1700  NEXT I
1710 '     NX,NY    DX,DY     SX,SY*n
1720 DATA  64,40,   96,136,   0,192, 64,192, 128,192
1730 '
1740 'ﾎﾝﾀｲ ｺﾋﾟｰｼﾞｭﾝﾋﾞ
1750 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
1760 A=7:VD(0,A)=SX:VD(1,A)=SY+SP:VD(2,A)=OX:VD(3,A)=OY+BP:VD(4,A)=SW:VD(5,A)=SH:VD(6,A)=0:VD(7,A)=&HD0 'HMMM
1770 '
1780 'CLOCK
1790 C$=STRING$(1,15)+STRING$(9,2)+STRING$(6,3)
1800 FOR I=1 TO 14
1810  A=VARPTR(SC(I*8))
1820  FOR J=0 TO 15:POKE A+J,ASC(MID$(C$,J+1,1)):NEXT J
1830 NEXT I
1840 FOR J=0 TO 7:SC(15*8+J)=&H101:NEXT J:SC(15*8+6)=&H401:SC(15*8+7)=&H507
1850 U=USR7(VARPTR(SC(0)))
1860 FORI=0TO31:SR(I*4)=216:NEXT I
1870 X=(256-8*20)\2
1880 FOR I=0 TO 7:J=I*4
1890  'PUTSPRITE I,(X+I*20,180),2,0
1900  'COLORSPRITE$(I)=STRING$(4,7)+STRING$(4,3)
1910  SR(J+0)=180*2
1920  SR(J+1)=(X+I*20)*2
1930  SR(J+2)=0
1940  SR(J+3)=0
1950 NEXT I
1960 '
1970 'EYE
1980 EX(0)=66:EY(0)=38:EX(1)=156:EY(1)=42
1990 FOR I=0 TO 1:J=I*4+8*4
2000  'PUTSPRITE 8+I,(EX(I),EY(I)),1,15
2010  SR(J+0)=EY(I)*2
2020  SR(J+1)=EX(I)*2
2030  SR(J+2)=15
2040  SR(J+3)=0
2050 NEXT I
2060 '
2070 'U=USR8(2) 'SPRITE SWAP MODE
2080 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
2090 '
2100 PN=7:GOSUB 2830'PALETTE
2110 TIME=0:FORI=0TO1:I=TIME:NEXT I
2120 LINE(0,192)-(255,211),0,BF
2130 '
2140 'ﾎﾝﾀｲ ｺﾋﾟｰ
2150 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
2160 A=7:VD(7,A)=&HD0:GOSUB 2720'HMMM
2170 '
2180 ONINTERVAL=5 GOSUB 2750:GOSUB 2750
2190 '
2200 'VDP(26)=2
2210 FORI=0TO7:PN=(7-I):GOSUB 2830:TIME=0:FORJ=0TO1:J=TIME:NEXT J:NEXT I
2220 F1=-1' 1ST FLAG
2230 EC=0:EI=60+RND(2)*60:RX=0:RY=0'EYE COUNT MOVE
2240 '
2250 'MAIN LOOP
2260 'U=USR5(0)'WAIT VDP COMMAND
2270 S=(1-(PEEK(KM+6)AND 2))*(16-(PEEK(KM+7)AND 16)) 'CTRL+STOP
2280 S=S+4-(PEEK(KM+7)AND4) 'ESC
2290 IF S THEN 3180 ' END
2300 '
2310 IF TIME<1 THEN 2310 ELSE TIME=0
2320 '
2330 ' (ANIMATION)
2340 AN=(AN+1)MOD(AM+1):A=AN:GOSUB 2690
2350 ' ACCEL
2360 ON 1+AE GOTO 2470
2370 ND$="device/accel/x":GOSUB 3040:X=A'_IOTGET(ND$,X)
2380 ND$="device/accel/y":GOSUB 3040:Y=A'_IOTGET(ND$,Y)
2390 ND$="device/accel/z":GOSUB 3040:Z=A'_IOTGET(ND$,Z)
2400 VX=X\32:VY=Y\32:VZ=Z\32
2410 ON 1-YM GOTO 2420:SWAP Y,Z:SWAP VY,VZ
2420 ON 1+(STRIG(0) OR STRIG(1) OR PAD(4) OR F1) GOTO 2470
2430 F1=0:BX=VX:BY=VY:IF ABS(Y)>800 THEN YM=1-YM:BY=VZ:SWAP VY,VZ
2440 EX=-VX+BX:EY=VY-BY 'ACCEL
2450 GOTO 2540
2460 ' TIME RND
2470 EC=EC+1:ON -(EC<EI) GOTO 2510
2480 EC=0:EI=60*RND(1)+60
2490 RX=255*RND(1)
2500 RY=211*RND(1)
2510 EX=(EX+(RX-128)/4)\2
2520 EY=(EY+(RY-106)/4)\2
2530 ' TOUCHPAD
2540 ON 1+PAD(4) GOTO 2580 'TOUCH PAD2
2550 EX=(EX+(PAD(5)-128)/4)\2
2560 EY=(EY+(PAD(6)-106)/4)\2
2570 '
2580 SR(8*4)  =EY(0)*2+EY
2590 SR(8*4+1)=EX(0)*2+EX
2600 SR(9*4  )=EY(1)*2+EY
2610 SR(9*4+1)=EX(1)*2+EX
2620 '
2630 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
2640 GOTO 2260
2650 '
2660 '------ COMMON
2670 '
2680 ' COPY VC
2690 U=USR4(VARPTR(VC(0,A))):RETURN
2700 '
2710 ' COPY VD
2720 U=USR4(VARPTR(VD(0,A))):RETURN
2730 '
2740 'TIMER
2750 INTERVAL OFF
2760 TC=(TC+1)AND 2047
2770 IF TC AND 3 THEN 2790
2780 U=USR3(VARPTR(SR(0))) 'TIME SPRITE
2790 INTERVAL ON
2800 RETURN
2810 '
2820 'PALETTE (PN=PALETTE NO)
2830 PJ=PN*16
2840 U=USR2(VARPTR(PL(PJ)))
2850 RETURN
2860 '
2870 '------ IOT
2880 '
2890 'NODE SELECT:ND$
2900 OUT 8,&HE0:OUT 8,1:OUT 8,&H53:IO$=ND$:GOSUB 2950
2910 NC=INP(8):ER=0:IF NC AND 128 THEN ER=19'ERROR 19'DEVICE I/O ERROR
2920 RETURN
2930 '
2940 'OUTPUT STRING(IO$)
2950 OUT 8,&HC0:NL=LEN(IO$):NH=1
2960 IF (NL-NH)<63 THEN 2980
2970 OUT 8,&H7F:FOR NI=NH TO NH+63:OUT 8,ASC(MID$(IO$,NI,1)):NEXT NI:NH=NH+63:GOTO 2960
2980 OUT 8,NL-NH+1:FOR NI=NH TO NL:OUT 8,ASC(MID$(IO$,NI,1)):NEXT NI:OUT 8,0:RETURN
2990 '
3000 'OUTPUT INT(A)
3010 OUT 8,&HC0:OUT 8,2:OUT 8,A AND 255:OUT 8,PEEK(VARPTR(A)+1):OUT 8,0:RETURN
3020 '
3030 'IOTGET ND$,A
3040 GOSUB 2900 'SELECT NODE
3050 OUT 8,&HE0:OUT 8,1:OUT 8,1'IOTGET($00)+INT(1)
3060 OUT 8,&H80:NL=INP(8):A=INP(8):POKE(VARPTR(A)+1),INP(8)'READ 2 BYTE
3070 RETURN
3080 '
3090 'IOTGET ND$,A$
3100 GOSUB 2900 'SELECT NODE
3110 OUT 8,&HE0:OUT 8,1:OUT 8,3'IOTGET($00)+STR(3)
3120 OUT 8,&H80:NL=INP(8)'READ DATA SIZE
3130 A$="":FOR NI=1 TO NL:A$=A$+CHR$(INP(8)):NEXT NI
3140 RETURN
3150 '
3160 '********************************
3170 '対応必須な処理は_TURBOOFF前に実行する
3180 INTERVAL OFF:U=USR8(-1):TR=0'TR=0にしておくとエラー発生で終了処理に飛ぶ
3190 '_TURBOOFFまでにCTRL+STOPが入力されていると_TURBOOFF直後に中断される
3200 _TURBOOFF
3210 'べーしっ君が無い場合はここに飛ばずにTR=0で3050へ飛ぶ
3220 ON ERROR GOTO 0:STOP OFF:END
3230 '********************************
3240 '
3250 '*** No XBASIC (-> NORMAL MODE)
3260 RESUME 3280
3270 ' TR=1でエラー発生した場合はノーマルモードとして続行する
3280 IF TR THEN TR=0:ON ERROR GOTO 0:STOP ON:GOTO 1420 'NORMAL MODEで続行
3290 ' TR=0でエラー発生した場合はこのまま終了処理へ
3300 '*** STOP hook (only NORMAL MODE)
3310 ON ERROR GOTO 0:STOP OFF:INTERVAL OFF:U=USR8(-1):END
