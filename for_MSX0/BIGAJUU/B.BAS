10 CLEAR 200,&HB000:DEFINT A-Z
20 AE=0:ON STOP GOSUB 90:STOP ON:ON ERROR GOTO 80
30 '_IOTPUT("msx/me/pm/cpu/percent",800)
40 DN$="device/i2c_i":_IOTFIND(DN$,CN):IF CN<1 THEN 110
50 DIM D$(CN-1):_IOTFIND(DN$,D$(0),CN)
60 FOR I=0 TO CN-1:AE=AE OR (D$(I)="68"):NEXT '68=MPU6886
70 GOTO 110
80 RESUME 110
90 '
100 ON ERROR GOTO 0:STOP OFF:U=USR8(-1):END
110 ON ERROR GOTO 0
120 SC=5:A=RND(-TIME)
130 COLOR 15,13,0:VDP(9)=VDP(9)AND253':VDP(9)=VDP(9)OR2
140 SCREEN SC':VDP(10)=VDP(10)AND 127'192 line mode
150 FOR I=0TO14:COLOR=(I,0,0,0):NEXT
160 DEFUSR=&H156
170 BLOAD"SRX2.BIN"
180 UA=&HD000:DIM UA(14):FOR I=0 TO 14:UA(I)=UA+I*3:NEXT
190 DEFUSR1=UA(0) 'LOAD SRD
200 DEFUSR2=UA(1) 'SET PAL
210 DEFUSR3=UA(2) 'TIMER SPRITE
220 DEFUSR4=UA(3) 'VDP CMD
230 DEFUSR5=UA(4) 'WAIT VDP CMD
240 DEFUSR6=UA(5) 'PUT SPRITE
250 DEFUSR7=UA(6) 'SET SPRITE PATTERN COLOR TABLE
260 DEFUSR8=UA(7) 'SET VSYNC HOOK for SPRITE (-1=RESTORE)
270 F$="BIGAJUU":F$=LEFT$(F$+SPACE$(8),8)
280 DIM PL(127):COPY F$+".PL"+HEX$(SC) TO PL
290 PL(0)=33:FOR I=1 TO 7:FOR J=0 TO 15:A=I*16+J:PL=PL(A-16):G=PL\256:R=(PL\16)AND 15:B=PL AND 15:B=(B-1)*-(B>0):R=(R-1)*-(R>0):G=(G-1)*-(G>0):PL(A)=B+R*16+G*256:NEXT:NEXT
300 SETPAGE,2:CLS:SETPAGE,3:CLS
310 SETPAGE0,1:A$=USR1(F$+".SR"+HEX$(SC)):SETPAGE0,0
320 SCREEN,3:BLOAD"SPRITE0.SC5",S,-(SC>6)*&H7800
330 '
340 'ÎÝÀ² ºËß° »· É »ÞË®³
350 OX=0:OY=0:SX=0:SY=0:SW=256:SH=192:SP=256:BP=768*0:DX=OX-SX:DY=OY-SY
360 '¼À É ±ÆÒ°¼®Ý
370 AM=2:XM=1-2*(SC<7)
560 DIM VD(7,7)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
380 DIM VC(7,7)' SX,SY, DX,DY, NX,NY, COLOR+ARG*256, CMD
390 RESTORE 540:READ NX,NY,DX,DY
410 FOR I=0 TO AM
420   READ CX,CY
430   VC(0,I)=CX:VC(1,I)=CY+SP 'SX,SY
440   VC(2,I)=DX+OX:VC(3,I)=DY+OY+BP 'DX,DY
450   VC(4,I)=NX:VC(5,I)=NY 'NX,NY
460   VC(6,I)=0 'COLOR+ARG*256
470   L=(VC(0,I)OR VC(2,I)OR VC(4,I))AND XM
480   IF L=0 THEN VC(7,I)=&HD0 'CMD HMMM
490   IF L THEN VC(7,I)=&H90 'CMD LMMM
500   'VC(7,J,I)=&H98 'CMD lMMM TPSET
510  NEXT
530 '     NX,NY    DX,DY     SX,SY*n
540 DATA  64,40,   96,136,   0,192, 64,192, 128,192
550 '
570 'ÎÝÀ² ºËß°¼Þ­ÝËÞ
580 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
590 A=7:VD(0,A)=SX:VD(1,A)=SY+SP:VD(2,A)=OX:VD(3,A)=OY+BP:VD(4,A)=SW:VD(5,A)=SH:VD(6,A)=0:VD(7,A)=&HD0 'HMMM
600 '
610 'CLOCK
620 DIM SC(16*8-1)'SPRITE COLOR by PATTERB NO
630 C$=STRING$(4,7)+STRING$(4,3)+STRING$(8,2)
640 FOR I=1 TO 14
650  A=VARPTR(SC(I*8))
660  FOR J=0 TO 15:POKE A+J,ASC(MID$(C$,J+1,1)):NEXT
670 NEXT
680 FOR J=0 TO 7:SC(15*8+J)=&H101:NEXT
690 U=USR7(VARPTR(SC(0)))
700 DIM SR(32*4-1):SA=&H7600'SPRITE ATTRIBUTE TABLE
701 FORI=0TO31:SR(I*4)=216:NEXT
710 X=(256-8*20)\2
720 FOR I=0 TO 7:J=I*4
730  PUTSPRITE I,(X+I*20,180),2,0
740  COLORSPRITE$(I)=STRING$(4,7)+STRING$(4,3)
750  SR(J+0)=VPEEK(SA+J+0)*2
760  SR(J+1)=VPEEK(SA+J+1)*2
770  SR(J+2)=VPEEK(SA+J+2)\4
780  SR(J+3)=0
790 NEXT
800 '
810 DIM EX(1),EY(1) ' EYE
820 EX(0)=66:EY(0)=38:EX(1)=156:EY(1)=42
830 FOR I=0 TO 1:J=I*4+8*4
840  PUTSPRITE 8+I,(EX(I),EY(I)),1,15
850  SR(J+0)=VPEEK(SA+J+0)*2
860  SR(J+1)=VPEEK(SA+J+1)*2
870  SR(J+2)=VPEEK(SA+J+2)\4
880  SR(J+3)=0
890 NEXT
900 '
901 PN=7:GOSUB 1520'PALETTE
902 TIME=0:FORI=0TO1:I=TIME:NEXT
910 LINE(0,192)-(255,211),0,BF
920 'ÎÝÀ² ºËß°
930 'COPY (SX,SY)-(SX+SW-1,SY+SH-1),1 TO (OX,OY),0
940 A=7:VD(7,A)=&HD0:GOSUB 1410'HMMM
950 '
960 ONINTERVAL=8 GOSUB 1440:GOSUB 1440
961 'U=USR8(2)
970 '
980 'A$="t80l8s0m20000o5 ge-o4b-o5e- fb-4. fgfo4b-o5e-2"
990 'B$="t80l8v13o4 r4g4o5d4.o4f b-4 b-4 sm20000g2"
1000 'PLAY A$,B$
1010 '
1020 FORI=0TO7:PN=(7-I):GOSUB 1520:TIME=0:FORJ=0TO1:J=TIME:NEXT:NEXT
1030 '
1040 TIME=0:TM=TIME:TB=TM
1050 'VDP(26)=2
1060 '
1070 'MAIN LOOP
1080 'U=USR5(0)'WAIT VDP COMMAND
1090 I$=INKEY$:A=USR(0)
1100 TIME=TIME AND 255:TM=TIME
1110 'ON -(TB>TM) GOTO 810
1120 TB=TM
1130 '
1140 ' (ANIMATION)
1150 AN=(AN+1)MOD(AM+1):A=AN:GOSUB 1380
1160 '
1170 ON 1+AE GOTO 1220
1180 _IOTGET("device/accel/x",X)
1190 _IOTGET("device/accel/y",Y)
1200 _IOTGET("device/accel/z",Z)
1201 VX=X\32:VY=Y\32:VZ=Z\32
1210 IF STRIG(0) OR STRIG(1) OR PAD(4) THEN BX=VX:BY=VY
1220 ON 1+PAD(4) GOTO 1250 'TOUCH PAD2
1230 EX=(EX+(PAD(5)-128)/8)\2
1240 EY=(EY+(PAD(6)-106)/6)\2
1245 GOTO 1260
1250 EX=-VX+BX:EY=VY-BY 'ACCEL
1260 SR(8*4)  =EY(0)*2+EY
1270 SR(8*4+1)=EX(0)*2+EX
1280 SR(9*4  )=EY(1)*2+EY
1290 SR(9*4+1)=EX(1)*2+EX
1300 '
1310 U=USR6(VARPTR(SR(0))) 'PUT SPRITE
1320 GOTO 1080
1340 '
1350 '------ COMMON
1360 '
1370 ' COPY VC
1380 U=USR4(VARPTR(VC(0,A))):RETURN
1390 '
1400 ' COPY VD
1410 U=USR4(VARPTR(VD(0,A))):RETURN
1420 '
1430 'TIMER
1440 INTERVAL OFF
1450 TC=(TC+1)AND 2047
1460 IF TC AND 3 THEN 1480
1470 U=USR3(VARPTR(SR(0))) 'TIME SPRITE
1480 INTERVAL ON
1490 RETURN
1500 '
1510 'PALETTE (PN=PALETTE NO)
1520 PJ=PN*16
1530 U=USR2(VARPTR(PL(PJ)))
1540 RETURN
1550 '
1560 '------ COMMON
