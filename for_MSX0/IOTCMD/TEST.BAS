10 CLEAR 400,&HD000:DEFINT A-Z
20 DIM AD(9)
30 BLOAD"IOTCMD.BIN"
40 AD=&HD000:FOR I=0 TO 9:AD(I)=AD+I*3:NEXT I
50 DEFUSR0=AD(0) 'set_xbasic
60 DEFUSR1=AD(1) 'iot_node
70 DEFUSR2=AD(2) 'iot_put_i
80 DEFUSR3=AD(3) 'iot_put_s
90 DEFUSR4=AD(4) 'iot_get_i
100 DEFUSR5=AD(5) 'iot_get_s
110 DEFUSR6=AD(6) 'iot_find_i
120 DEFUSR7=AD(7) 'iot_find_s
130 DEFUSR8=AD(8) 'iot_read_i
140 DEFUSR9=AD(9) 'iot_read_s
150 '
160 SCREEN 0
170 '
180 _TURBO ON(AD())
190 IF 1E-40=0 THEN A=USR(1):PRINT"TURBO"
191 '
200 '対象ノードを指定
210 ND$="device/i2c_i":A=USR1(VARPTR(ND$)) 'IOT_NODE
220 PRINT "SET NODE:";ND$;" - ";
230 IF A THEN PRINT"ERROR:";A ELSE PRINT"OK"
240 '
250 'サブノードの数を取得
260 CN=USR6(0) ' IOT_FIND_I
270 PRINT "FIND_I=";CN
280 BF$=STRING$(32, 32)
290 '
300 '1つ目のサブノード取得
310 D$=BF$:A=USR7(VARPTR(D$)):D$=LEFT$(D$,A)
320 PRINT"FIND_S(";A;") ";D$
330 IF D$="68" THEN MU=1'ジャイロあり
340 '
350 '2つ目以降のサブノード取得(あれば)
360 FOR I=1 TO CN-1
370 D$=BF$:A=USR9(VARPTR(D$)):D$=LEFT$(D$,A)
380  PRINT"READ_S(";A;") ";D$
390  IF D$="68" THEN MU=1'ジャイロあり
400 NEXT I
410 '
420 'ジャイロ発見？
430 PRINT"68:MPU6886(ACCELL) ";
440 IF MU=0 THEN PRINT"not "
450 PRINT"found"
460 IF MU=0 THEN END
470 '
530 AX$="device/accel/x":A=USR1(VARPTR(AX$)) 'IOT_NODE
540 PRINT "SET NODE:";AX$;" - ";
550 IF A THEN PRINT"ERROR:";A ELSE PRINT"OK"
70 '
580 AY$="device/accel/y":A=USR1(VARPTR(AY$)) 'IOT_NODE
590 PRINT "SET NODE:";AY$;" - ";
600 IF A THEN PRINT"ERROR:";A ELSE PRINT"OK"
620 '
630 AZ$="device/accel/z":A=USR1(VARPTR(AZ$)) 'IOT_NODE
640 PRINT "SET NODE:";AZ$;" - ";
650 IF A THEN PRINT"ERROR:";A ELSE PRINT"OK"
670 '
680 CX=POS(0):CY=CSRLIN'現在の位置
690 '
700 'ジャイロ表示
710 LOCATE CX,CY
720 A=USR1(VARPTR(AX$)) 'IOT_NODE device/accel/x
730 X=USR4(0):PRINT"X=";X;"   "'IOT_GET_I
740 A=USR1(VARPTR(AY$)) 'IOT_NODE device/accel/y
750 Y=USR4(0):PRINT"Y=";Y;"   "'IOT_GET_I
760 A=USR1(VARPTR(AZ$)) 'IOT_NODE device/accel/z
770 Z=USR4(0):PRINT"Z=";Z;"   "'IOT_GET_I
780 T=1-T:PRINT MID$("*-",T+1,1)
790 IF INKEY$="" THEN 710
